{{
  "language": "Solidity",
  "settings": {
    "evmVersion": "paris",
    "libraries": {},
    "metadata": {
      "bytecodeHash": "ipfs",
      "useLiteralContent": true
    },
    "optimizer": {
      "enabled": true,
      "runs": 200
    },
    "remappings": [],
    "outputSelection": {
      "*": {
        "*": [
          "evm.bytecode",
          "evm.deployedBytecode",
          "devdoc",
          "userdoc",
          "metadata",
          "abi"
        ]
      }
    }
  },
  "sources": {
    "@lazy-sol/access-control-upgradeable/contracts/InitializableAccessControlCore.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity >=0.8.4; // custom errors (0.8.4)\n\nimport \"@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol\";\n\n/**\n * @title Initializable Role-based Access Control Core (I-RBAC-C)\n *\n * @notice Access control smart contract provides an API to check\n *      if a specific operation is permitted globally and/or\n *      if a particular user has a permission to execute it.\n *\n * @notice This contract is inherited by other contracts requiring the role-based access control (RBAC)\n *      protection for the restricted access functions\n *\n * @notice It deals with two main entities: features and roles.\n *\n * @notice Features are designed to be used to enable/disable public functions\n *      of the smart contract (used by a wide audience).\n * @notice User roles are designed to control the access to restricted functions\n *      of the smart contract (used by a limited set of maintainers).\n *\n * @notice Terms \"role\", \"permissions\" and \"set of permissions\" have equal meaning\n *      in the documentation text and may be used interchangeably.\n * @notice Terms \"permission\", \"single permission\" implies only one permission bit set.\n *\n * @notice Access manager is a special role which allows to grant/revoke other roles.\n *      Access managers can only grant/revoke permissions which they have themselves.\n *      As an example, access manager with no other roles set can only grant/revoke its own\n *      access manager permission and nothing else.\n *\n * @notice Access manager permission should be treated carefully, as a super admin permission:\n *      Access manager with even no other permission can interfere with another account by\n *      granting own access manager permission to it and effectively creating more powerful\n *      permission set than its own.\n *\n * @dev Both current and OpenZeppelin AccessControl implementations feature a similar API\n *      to check/know \"who is allowed to do this thing\".\n * @dev Zeppelin implementation is more flexible:\n *      - it allows setting unlimited number of roles, while current is limited to 256 different roles\n *      - it allows setting an admin for each role, while current allows having only one global admin\n * @dev Current implementation is more lightweight:\n *      - it uses only 1 bit per role, while Zeppelin uses 256 bits\n *      - it allows setting up to 256 roles at once, in a single transaction, while Zeppelin allows\n *        setting only one role in a single transaction\n *\n * @dev This smart contract is designed to be inherited by other\n *      smart contracts which require access control management capabilities.\n *\n * @dev Access manager permission has a bit 255 set.\n *      This bit must not be used by inheriting contracts for any other permissions/features.\n *\n * @dev This is an initializable version of the RBAC, based on Zeppelin implementation,\n *      it can be used for EIP-1167 minimal proxies, for ERC1967 proxies, etc.\n *      see https://docs.openzeppelin.com/contracts/4.x/api/proxy#Clones\n *      see https://docs.openzeppelin.com/contracts/4.x/upgradeable\n *      see https://docs.openzeppelin.com/contracts/4.x/api/proxy#UUPSUpgradeable\n *      see https://forum.openzeppelin.com/t/uups-proxies-tutorial-solidity-javascript/7786\n *      see https://eips.ethereum.org/EIPS/eip-1167\n *\n * @dev The 'core' version of the RBAC contract hides three rarely used external functions from the public ABI,\n *      making them internal and thus reducing the overall compiled implementation size.\n *      isFeatureEnabled() public -> _isFeatureEnabled() internal\n *      isSenderInRole() public -> _isSenderInRole() internal\n *      isOperatorInRole() public -> _isOperatorInRole() internal\n *\n * @custom:since 1.1.0\n *\n * @author Basil Gorin\n */\nabstract contract InitializableAccessControlCore is Initializable {\n\t/**\n\t * @dev Privileged addresses with defined roles/permissions\n\t * @dev In the context of ERC20/ERC721 tokens these can be permissions to\n\t *      allow minting or burning tokens, transferring on behalf and so on\n\t *\n\t * @dev Maps user address to the permissions bitmask (role), where each bit\n\t *      represents a permission\n\t * @dev Bitmask 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF\n\t *      represents all possible permissions\n\t * @dev 'This' address mapping represents global features of the smart contract\n\t *\n\t * @dev We keep the mapping private to prevent direct writes to it from the inheriting\n\t *      contracts, `getRole()` and `updateRole()` functions should be used instead\n\t */\n\tmapping(address => uint256) private userRoles;\n\n\t/**\n\t * @dev Empty reserved space in storage. The size of the __gap array is calculated so that\n\t *      the amount of storage used by a contract always adds up to the 50.\n\t *      See https://docs.openzeppelin.com/contracts/4.x/upgradeable#storage_gaps\n\t */\n\tuint256[49] private __gap;\n\n\t/**\n\t * @notice Access manager is responsible for assigning the roles to users,\n\t *      enabling/disabling global features of the smart contract\n\t * @notice Access manager can add, remove and update user roles,\n\t *      remove and update global features\n\t *\n\t * @dev Role ROLE_ACCESS_MANAGER allows modifying user roles and global features\n\t * @dev Role ROLE_ACCESS_MANAGER has single bit at position 255 enabled\n\t */\n\tuint256 public constant ROLE_ACCESS_MANAGER = 0x8000000000000000000000000000000000000000000000000000000000000000;\n\n\t/**\n\t * @notice Upgrade manager is responsible for smart contract upgrades,\n\t *      see https://docs.openzeppelin.com/contracts/4.x/api/proxy#UUPSUpgradeable\n\t *      see https://docs.openzeppelin.com/contracts/4.x/upgradeable\n\t *\n\t * @dev Role ROLE_UPGRADE_MANAGER allows passing the _authorizeUpgrade() check\n\t * @dev Role ROLE_UPGRADE_MANAGER has single bit at position 254 enabled\n\t */\n\tuint256 public constant ROLE_UPGRADE_MANAGER = 0x4000000000000000000000000000000000000000000000000000000000000000;\n\n\t/**\n\t * @dev Bitmask representing all the possible permissions (super admin role)\n\t * @dev Has all the bits are enabled (2^256 - 1 value)\n\t */\n\tuint256 internal constant FULL_PRIVILEGES_MASK = type(uint256).max; // before 0.8.0: uint256(-1) overflows to 0xFFFF...\n\n\t/**\n\t * @notice Thrown when a function is executed by an account that does not have\n\t *      the required access permission(s) (role)\n\t *\n\t * @dev This error is used to enforce role-based access control (RBAC) restrictions\n\t */\n\terror AccessDenied();\n\n\t/**\n\t * @dev Fired in updateRole() and updateFeatures()\n\t *\n\t * @param operator address which was granted/revoked permissions\n\t * @param requested permissions requested\n\t * @param assigned permissions effectively set\n\t */\n\tevent RoleUpdated(address indexed operator, uint256 requested, uint256 assigned);\n\n\t/**\n\t * @notice Function modifier making a function defined as public behave as restricted\n\t *      (so that only a pre-configured set of accounts can execute it)\n\t *\n\t * @param role the role transaction executor is required to have;\n\t *      the function throws an \"access denied\" exception if this condition is not met\n\t */\n\tmodifier restrictedTo(uint256 role) {\n\t\t// verify the access permission\n\t\t_requireSenderInRole(role);\n\n\t\t// execute the rest of the function\n\t\t_;\n\t}\n\n\t/**\n\t * @dev Creates/deploys the RBAC implementation to be used in a proxy\n\t *\n\t * @dev Note:\n\t *      the implementation is already initialized and\n\t *      `_postConstruct` is not executable on the implementation\n\t *      `_postConstruct` is still available in the context of a proxy\n\t *      and should be executed on the proxy deployment (in the same tx)\n\t */\n\tconstructor() initializer {}\n\n\t/**\n\t * @dev Contract initializer, sets the contract owner to have full privileges\n\t *\n\t * @dev Can be executed only once, reverts when executed second time\n\t *\n\t * @dev IMPORTANT:\n\t *      this function SHOULD be executed during proxy deployment (in the same transaction)\n\t *\n\t * @param _owner smart contract owner having full privileges, can be zero\n\t * @param _features initial features mask of the contract, can be zero\n\t */\n\tfunction _postConstruct(address _owner, uint256 _features) internal virtual onlyInitializing {\n\t\t// grant owner full privileges\n\t\t__setRole(_owner, FULL_PRIVILEGES_MASK, FULL_PRIVILEGES_MASK);\n\t\t// update initial features bitmask\n\t\t__setRole(address(this), _features, _features);\n\t}\n\n\t/**\n\t * @dev Highest version that has been initialized.\n\t *      Non-zero value means contract was already initialized.\n\t * @dev see {Initializable}, {reinitializer}.\n\t *\n\t * @return highest version that has been initialized\n\t */\n\tfunction getInitializedVersion() public view returns(uint64) {\n\t\t// delegate to `_getInitializedVersion`\n\t\treturn _getInitializedVersion();\n\t}\n\n\t/**\n\t * @notice Retrieves globally set of features enabled\n\t *\n\t * @dev Effectively reads userRoles role for the contract itself\n\t *\n\t * @return 256-bit bitmask of the features enabled\n\t */\n\tfunction features() public view returns (uint256) {\n\t\t// features are stored in 'this' address mapping of `userRoles`\n\t\treturn getRole(address(this));\n\t}\n\n\t/**\n\t * @notice Updates set of the globally enabled features (`features`),\n\t *      taking into account sender's permissions\n\t *\n\t * @dev Requires transaction sender to have `ROLE_ACCESS_MANAGER` permission\n\t * @dev Function is left for backward compatibility with older versions\n\t *\n\t * @param _mask bitmask representing a set of features to enable/disable\n\t */\n\tfunction updateFeatures(uint256 _mask) public {\n\t\t// delegate call to `updateRole`\n\t\tupdateRole(address(this), _mask);\n\t}\n\n\t/**\n\t * @notice Reads the permissions (role) for a given user from the `userRoles` mapping\n\t *      (privileged addresses with defined roles/permissions)\n\t * @notice In the context of ERC20/ERC721 tokens these can be permissions to\n\t *      allow minting or burning tokens, transferring on behalf and so on\n\t *\n\t * @dev Having a simple getter instead of making the mapping public\n\t *      allows enforcing the encapsulation of the mapping and protects from\n\t *      writing to it directly in the inheriting smart contracts\n\t *\n\t * @param operator address of a user to read permissions for,\n\t *      or self address to read global features of the smart contract\n\t */\n\tfunction getRole(address operator) public view returns(uint256) {\n\t\t// read the value from `userRoles` and return\n\t\treturn userRoles[operator];\n\t}\n\n\t/**\n\t * @notice Updates set of permissions (role) for a given user,\n\t *      taking into account sender's permissions.\n\t *\n\t * @dev Setting role to zero is equivalent to removing an all permissions\n\t * @dev Setting role to `FULL_PRIVILEGES_MASK` is equivalent to\n\t *      copying senders' permissions (role) to the user\n\t * @dev Requires transaction sender to have `ROLE_ACCESS_MANAGER` permission\n\t *\n\t * @param operator address of a user to alter permissions for,\n\t *       or self address to alter global features of the smart contract\n\t * @param role bitmask representing a set of permissions to\n\t *      enable/disable for a user specified\n\t */\n\tfunction updateRole(address operator, uint256 role) public {\n\t\t// caller must have a permission to update user roles\n\t\t_requireSenderInRole(ROLE_ACCESS_MANAGER);\n\n\t\t// evaluate the role and reassign it\n\t\t__setRole(operator, role, _evaluateBy(msg.sender, getRole(operator), role));\n\t}\n\n\t/**\n\t * @notice Determines the permission bitmask an operator can set on the\n\t *      target permission set\n\t * @notice Used to calculate the permission bitmask to be set when requested\n\t *     in `updateRole` and `updateFeatures` functions\n\t *\n\t * @dev Calculated based on:\n\t *      1) operator's own permission set read from userRoles[operator]\n\t *      2) target permission set - what is already set on the target\n\t *      3) desired permission set - what do we want set target to\n\t *\n\t * @dev Corner cases:\n\t *      1) Operator is super admin and its permission set is `FULL_PRIVILEGES_MASK`:\n\t *        `desired` bitset is returned regardless of the `target` permission set value\n\t *        (what operator sets is what they get)\n\t *      2) Operator with no permissions (zero bitset):\n\t *        `target` bitset is returned regardless of the `desired` value\n\t *        (operator has no authority and cannot modify anything)\n\t *\n\t * @dev Example:\n\t *      Consider an operator with the permissions bitmask     00001111\n\t *      is about to modify the target permission set          01010101\n\t *      Operator wants to set that permission set to          00110011\n\t *      Based on their role, an operator has the permissions\n\t *      to update only lowest 4 bits on the target, meaning that\n\t *      high 4 bits of the target set in this example is left\n\t *      unchanged and low 4 bits get changed as desired:      01010011\n\t *\n\t * @param operator address of the contract operator which is about to set the permissions\n\t * @param target input set of permissions to operator is going to modify\n\t * @param desired desired set of permissions operator would like to set\n\t * @return resulting set of permissions given operator will set\n\t */\n\tfunction _evaluateBy(address operator, uint256 target, uint256 desired) internal view returns (uint256) {\n\t\t// read operator's permissions\n\t\tuint256 p = getRole(operator);\n\n\t\t// taking into account operator's permissions,\n\t\t// 1) enable the permissions desired on the `target`\n\t\ttarget |= p & desired;\n\t\t// 2) disable the permissions desired on the `target`\n\t\ttarget &= FULL_PRIVILEGES_MASK ^ (p & (FULL_PRIVILEGES_MASK ^ desired));\n\n\t\t// return calculated result\n\t\treturn target;\n\t}\n\n\t/**\n\t * @notice Ensures that the transaction sender has the required access permission(s) (role)\n\t *\n\t * @dev Reverts with an `AccessDenied` error if the sender does not have the required role\n\t *\n\t * @param required the set of permissions (role) that the transaction sender is required to have\n\t */\n\tfunction _requireSenderInRole(uint256 required) internal view {\n\t\t// check if the transaction has the required permission(s),\n\t\t// reverting with the \"access denied\" error if not\n\t\t_requireAccessCondition(_isSenderInRole(required));\n\t}\n\n\t/**\n\t * @notice Ensures that a specific condition is met\n\t *\n\t * @dev Reverts with an `AccessDenied` error if the condition is not met\n\t *\n\t * @param condition the condition that needs to be true for the function to proceed\n\t */\n\tfunction _requireAccessCondition(bool condition) internal pure {\n\t\t// check if the condition holds\n\t\tif(!condition) {\n\t\t\t// revert with the \"access denied\" error if not\n\t\t\trevert AccessDenied();\n\t\t}\n\t}\n\n\t/**\n\t * @notice Checks if requested set of features is enabled globally on the contract\n\t *\n\t * @param required set of features to check against\n\t * @return true if all the features requested are enabled, false otherwise\n\t */\n\tfunction _isFeatureEnabled(uint256 required) internal view returns (bool) {\n\t\t// delegate call to `__hasRole`, passing `features` property\n\t\treturn __hasRole(features(), required);\n\t}\n\n\t/**\n\t * @notice Checks if transaction sender `msg.sender` has all the permissions required\n\t *\n\t * @param required set of permissions (role) to check against\n\t * @return true if all the permissions requested are enabled, false otherwise\n\t */\n\tfunction _isSenderInRole(uint256 required) internal view returns (bool) {\n\t\t// delegate call to `isOperatorInRole`, passing transaction sender\n\t\treturn _isOperatorInRole(msg.sender, required);\n\t}\n\n\t/**\n\t * @notice Checks if operator has all the permissions (role) required\n\t *\n\t * @param operator address of the user to check role for\n\t * @param required set of permissions (role) to check\n\t * @return true if all the permissions requested are enabled, false otherwise\n\t */\n\tfunction _isOperatorInRole(address operator, uint256 required) internal view returns (bool) {\n\t\t// delegate call to `__hasRole`, passing operator's permissions (role)\n\t\treturn __hasRole(getRole(operator), required);\n\t}\n\n\t/**\n\t * @dev Sets the `assignedRole` role to the operator, logs both `requestedRole` and `actualRole`\n\t *\n\t * @dev Unsafe:\n\t *      provides direct write access to `userRoles` mapping without any security checks,\n\t *      doesn't verify the executor (msg.sender) permissions,\n\t *      must be kept private at all times\n\t *\n\t * @param operator address of a user to alter permissions for,\n\t *       or self address to alter global features of the smart contract\n\t * @param requestedRole bitmask representing a set of permissions requested\n\t *      to be enabled/disabled for a user specified, used only to be logged into event\n\t * @param assignedRole bitmask representing a set of permissions to\n\t *      enable/disable for a user specified, used to update the mapping and to be logged into event\n\t */\n\tfunction __setRole(address operator, uint256 requestedRole, uint256 assignedRole) private {\n\t\t// assign the role to the operator\n\t\tuserRoles[operator] = assignedRole;\n\n\t\t// fire an event\n\t\temit RoleUpdated(operator, requestedRole, assignedRole);\n\t}\n\n\t/**\n\t * @dev Checks if role `actual` contains all the permissions required `required`\n\t *\n\t * @param actual existent role\n\t * @param required required role\n\t * @return true if actual has required role (all permissions), false otherwise\n\t */\n\tfunction __hasRole(uint256 actual, uint256 required) private pure returns (bool) {\n\t\t// check the bitmask for the role required and return the result\n\t\treturn actual & required == required;\n\t}\n}\n"
    },
    "@lazy-sol/access-control-upgradeable/contracts/UpgradeableAccessControlCore.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity >=0.8.4;\n\nimport \"./InitializableAccessControlCore.sol\";\nimport \"@openzeppelin/contracts-upgradeable/proxy/utils/UUPSUpgradeable.sol\";\n\n/**\n * @title Upgradeable Role-based Access Control Core (U-RBAC-C) // ERC1967Proxy\n *\n * @notice Access control smart contract provides an API to check\n *      if a specific operation is permitted globally and/or\n *      if a particular user has a permission to execute it.\n *\n * @notice This contract is inherited by other contracts requiring the role-based access control (RBAC)\n *      protection for the restricted access functions\n *\n * @notice It deals with two main entities: features and roles.\n *\n * @notice Features are designed to be used to enable/disable public functions\n *      of the smart contract (used by a wide audience).\n * @notice User roles are designed to control the access to restricted functions\n *      of the smart contract (used by a limited set of maintainers).\n *\n * @notice Terms \"role\", \"permissions\" and \"set of permissions\" have equal meaning\n *      in the documentation text and may be used interchangeably.\n * @notice Terms \"permission\", \"single permission\" implies only one permission bit set.\n *\n * @notice Access manager is a special role which allows to grant/revoke other roles.\n *      Access managers can only grant/revoke permissions which they have themselves.\n *      As an example, access manager with no other roles set can only grant/revoke its own\n *      access manager permission and nothing else.\n *\n * @notice Access manager permission should be treated carefully, as a super admin permission:\n *      Access manager with even no other permission can interfere with another account by\n *      granting own access manager permission to it and effectively creating more powerful\n *      permission set than its own.\n *\n * @dev Both current and OpenZeppelin AccessControl implementations feature a similar API\n *      to check/know \"who is allowed to do this thing\".\n * @dev Zeppelin implementation is more flexible:\n *      - it allows setting unlimited number of roles, while current is limited to 256 different roles\n *      - it allows setting an admin for each role, while current allows having only one global admin\n * @dev Current implementation is more lightweight:\n *      - it uses only 1 bit per role, while Zeppelin uses 256 bits\n *      - it allows setting up to 256 roles at once, in a single transaction, while Zeppelin allows\n *        setting only one role in a single transaction\n *\n * @dev This smart contract is designed to be inherited by other\n *      smart contracts which require access control management capabilities.\n *\n * @dev Access manager permission has a bit 255 set.\n *      This bit must not be used by inheriting contracts for any other permissions/features.\n *\n * @dev This is an upgradeable version of the RBAC, based on Zeppelin implementation for ERC1967,\n *      see https://docs.openzeppelin.com/contracts/4.x/upgradeable\n *      see https://docs.openzeppelin.com/contracts/4.x/api/proxy#UUPSUpgradeable\n *      see https://forum.openzeppelin.com/t/uups-proxies-tutorial-solidity-javascript/7786\n *\n * @dev The 'core' version of the RBAC contract hides three rarely used external functions from the public ABI,\n *      making them internal and thus reducing the overall compiled implementation size.\n *      isFeatureEnabled() public -> _isFeatureEnabled() internal\n *      isSenderInRole() public -> _isSenderInRole() internal\n *      isOperatorInRole() public -> _isOperatorInRole() internal\n *\n * @custom:since 1.1.0\n *\n * @author Basil Gorin\n */\nabstract contract UpgradeableAccessControlCore is InitializableAccessControlCore, UUPSUpgradeable {\n\t/**\n\t * @notice Returns an address of the implementation smart contract,\n\t *      see ERC1967Upgrade._getImplementation()\n\t *\n\t * @return the current implementation address\n\t */\n\tfunction getImplementation() public view virtual returns (address) {\n\t\t// delegate to `ERC1967Upgrade._getImplementation()`\n\t\treturn _getImplementation();\n\t}\n\n\t/**\n\t * @inheritdoc UUPSUpgradeable\n\t */\n\tfunction _authorizeUpgrade(address) internal virtual override {\n\t\t// caller must have a permission to upgrade the contract\n\t\t_requireSenderInRole(ROLE_UPGRADE_MANAGER);\n\t}\n}\n"
    },
    "@lazy-sol/advanced-erc20/contracts/lib/ECDSA.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.4;\n\n/**\n * @dev Elliptic Curve Digital Signature Algorithm (ECDSA) operations.\n *\n * These functions can be used to verify that a message was signed by the holder\n * of the private keys of a given address.\n *\n * @dev Copy of the Zeppelin's library:\n *      https://github.com/OpenZeppelin/openzeppelin-contracts/blob/b0cf6fbb7a70f31527f36579ad644e1cf12fdf4e/contracts/utils/cryptography/ECDSA.sol\n */\nlibrary ECDSA {\n\t/**\n\t * @dev Overload of {ECDSA-recover} that receives the `v`,\n\t * `r` and `s` signature fields separately.\n\t */\n\tfunction recover(\n\t\tbytes32 hash,\n\t\tuint8 v,\n\t\tbytes32 r,\n\t\tbytes32 s\n\t) internal pure returns (address) {\n\t\t// EIP-2 still allows signature malleability for ecrecover(). Remove this possibility and make the signature\n\t\t// unique. Appendix F in the Ethereum Yellow paper (https://ethereum.github.io/yellowpaper/paper.pdf), defines\n\t\t// the valid range for s in (281): 0 < s < secp256k1n ÷ 2 + 1, and for v in (282): v ∈ {27, 28}. Most\n\t\t// signatures from current libraries generate a unique signature with an s-value in the lower half order.\n\t\t//\n\t\t// If your library generates malleable signatures, such as s-values in the upper range, calculate a new s-value\n\t\t// with 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFEBAAEDCE6AF48A03BBFD25E8CD0364141 - s1 and flip v from 27 to 28 or\n\t\t// vice versa. If your library also generates signatures with 0/1 for v instead 27/28, add 27 to v to accept\n\t\t// these malleable signatures as well.\n\t\trequire(\n\t\t\tuint256(s) <= 0x7FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF5D576E7357A4501DDFE92F46681B20A0,\n\t\t\t\"invalid signature 's' value\"\n\t\t);\n\t\trequire(v == 27 || v == 28, \"invalid signature 'v' value\");\n\n\t\t// If the signature is valid (and not malleable), return the signer address\n\t\taddress signer = ecrecover(hash, v, r, s);\n\t\trequire(signer != address(0), \"invalid signature\");\n\n\t\treturn signer;\n\t}\n}\n"
    },
    "@openzeppelin/contracts-upgradeable/interfaces/IERC1967Upgradeable.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.9.0) (interfaces/IERC1967.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev ERC-1967: Proxy Storage Slots. This interface contains the events defined in the ERC.\n *\n * _Available since v4.8.3._\n */\ninterface IERC1967Upgradeable {\n    /**\n     * @dev Emitted when the implementation is upgraded.\n     */\n    event Upgraded(address indexed implementation);\n\n    /**\n     * @dev Emitted when the admin account has changed.\n     */\n    event AdminChanged(address previousAdmin, address newAdmin);\n\n    /**\n     * @dev Emitted when the beacon is changed.\n     */\n    event BeaconUpgraded(address indexed beacon);\n}\n"
    },
    "@openzeppelin/contracts-upgradeable/interfaces/draft-IERC1822Upgradeable.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.5.0) (interfaces/draft-IERC1822.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev ERC1822: Universal Upgradeable Proxy Standard (UUPS) documents a method for upgradeability through a simplified\n * proxy whose upgrades are fully controlled by the current implementation.\n */\ninterface IERC1822ProxiableUpgradeable {\n    /**\n     * @dev Returns the storage slot that the proxiable contract assumes is being used to store the implementation\n     * address.\n     *\n     * IMPORTANT: A proxy pointing at a proxiable contract should not be considered proxiable itself, because this risks\n     * bricking a proxy that upgrades to it, by delegating to itself until out of gas. Thus it is critical that this\n     * function revert if invoked through a proxy.\n     */\n    function proxiableUUID() external view returns (bytes32);\n}\n"
    },
    "@openzeppelin/contracts-upgradeable/proxy/ERC1967/ERC1967UpgradeUpgradeable.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.9.0) (proxy/ERC1967/ERC1967Upgrade.sol)\n\npragma solidity ^0.8.2;\n\nimport \"../beacon/IBeaconUpgradeable.sol\";\nimport \"../../interfaces/IERC1967Upgradeable.sol\";\nimport \"../../interfaces/draft-IERC1822Upgradeable.sol\";\nimport \"../../utils/AddressUpgradeable.sol\";\nimport \"../../utils/StorageSlotUpgradeable.sol\";\nimport \"../utils/Initializable.sol\";\n\n/**\n * @dev This abstract contract provides getters and event emitting update functions for\n * https://eips.ethereum.org/EIPS/eip-1967[EIP1967] slots.\n *\n * _Available since v4.1._\n */\nabstract contract ERC1967UpgradeUpgradeable is Initializable, IERC1967Upgradeable {\n    function __ERC1967Upgrade_init() internal onlyInitializing {\n    }\n\n    function __ERC1967Upgrade_init_unchained() internal onlyInitializing {\n    }\n    // This is the keccak-256 hash of \"eip1967.proxy.rollback\" subtracted by 1\n    bytes32 private constant _ROLLBACK_SLOT = 0x4910fdfa16fed3260ed0e7147f7cc6da11a60208b5b9406d12a635614ffd9143;\n\n    /**\n     * @dev Storage slot with the address of the current implementation.\n     * This is the keccak-256 hash of \"eip1967.proxy.implementation\" subtracted by 1, and is\n     * validated in the constructor.\n     */\n    bytes32 internal constant _IMPLEMENTATION_SLOT = 0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc;\n\n    /**\n     * @dev Returns the current implementation address.\n     */\n    function _getImplementation() internal view returns (address) {\n        return StorageSlotUpgradeable.getAddressSlot(_IMPLEMENTATION_SLOT).value;\n    }\n\n    /**\n     * @dev Stores a new address in the EIP1967 implementation slot.\n     */\n    function _setImplementation(address newImplementation) private {\n        require(AddressUpgradeable.isContract(newImplementation), \"ERC1967: new implementation is not a contract\");\n        StorageSlotUpgradeable.getAddressSlot(_IMPLEMENTATION_SLOT).value = newImplementation;\n    }\n\n    /**\n     * @dev Perform implementation upgrade\n     *\n     * Emits an {Upgraded} event.\n     */\n    function _upgradeTo(address newImplementation) internal {\n        _setImplementation(newImplementation);\n        emit Upgraded(newImplementation);\n    }\n\n    /**\n     * @dev Perform implementation upgrade with additional setup call.\n     *\n     * Emits an {Upgraded} event.\n     */\n    function _upgradeToAndCall(address newImplementation, bytes memory data, bool forceCall) internal {\n        _upgradeTo(newImplementation);\n        if (data.length > 0 || forceCall) {\n            AddressUpgradeable.functionDelegateCall(newImplementation, data);\n        }\n    }\n\n    /**\n     * @dev Perform implementation upgrade with security checks for UUPS proxies, and additional setup call.\n     *\n     * Emits an {Upgraded} event.\n     */\n    function _upgradeToAndCallUUPS(address newImplementation, bytes memory data, bool forceCall) internal {\n        // Upgrades from old implementations will perform a rollback test. This test requires the new\n        // implementation to upgrade back to the old, non-ERC1822 compliant, implementation. Removing\n        // this special case will break upgrade paths from old UUPS implementation to new ones.\n        if (StorageSlotUpgradeable.getBooleanSlot(_ROLLBACK_SLOT).value) {\n            _setImplementation(newImplementation);\n        } else {\n            try IERC1822ProxiableUpgradeable(newImplementation).proxiableUUID() returns (bytes32 slot) {\n                require(slot == _IMPLEMENTATION_SLOT, \"ERC1967Upgrade: unsupported proxiableUUID\");\n            } catch {\n                revert(\"ERC1967Upgrade: new implementation is not UUPS\");\n            }\n            _upgradeToAndCall(newImplementation, data, forceCall);\n        }\n    }\n\n    /**\n     * @dev Storage slot with the admin of the contract.\n     * This is the keccak-256 hash of \"eip1967.proxy.admin\" subtracted by 1, and is\n     * validated in the constructor.\n     */\n    bytes32 internal constant _ADMIN_SLOT = 0xb53127684a568b3173ae13b9f8a6016e243e63b6e8ee1178d6a717850b5d6103;\n\n    /**\n     * @dev Returns the current admin.\n     */\n    function _getAdmin() internal view returns (address) {\n        return StorageSlotUpgradeable.getAddressSlot(_ADMIN_SLOT).value;\n    }\n\n    /**\n     * @dev Stores a new address in the EIP1967 admin slot.\n     */\n    function _setAdmin(address newAdmin) private {\n        require(newAdmin != address(0), \"ERC1967: new admin is the zero address\");\n        StorageSlotUpgradeable.getAddressSlot(_ADMIN_SLOT).value = newAdmin;\n    }\n\n    /**\n     * @dev Changes the admin of the proxy.\n     *\n     * Emits an {AdminChanged} event.\n     */\n    function _changeAdmin(address newAdmin) internal {\n        emit AdminChanged(_getAdmin(), newAdmin);\n        _setAdmin(newAdmin);\n    }\n\n    /**\n     * @dev The storage slot of the UpgradeableBeacon contract which defines the implementation for this proxy.\n     * This is bytes32(uint256(keccak256('eip1967.proxy.beacon')) - 1)) and is validated in the constructor.\n     */\n    bytes32 internal constant _BEACON_SLOT = 0xa3f0ad74e5423aebfd80d3ef4346578335a9a72aeaee59ff6cb3582b35133d50;\n\n    /**\n     * @dev Returns the current beacon.\n     */\n    function _getBeacon() internal view returns (address) {\n        return StorageSlotUpgradeable.getAddressSlot(_BEACON_SLOT).value;\n    }\n\n    /**\n     * @dev Stores a new beacon in the EIP1967 beacon slot.\n     */\n    function _setBeacon(address newBeacon) private {\n        require(AddressUpgradeable.isContract(newBeacon), \"ERC1967: new beacon is not a contract\");\n        require(\n            AddressUpgradeable.isContract(IBeaconUpgradeable(newBeacon).implementation()),\n            \"ERC1967: beacon implementation is not a contract\"\n        );\n        StorageSlotUpgradeable.getAddressSlot(_BEACON_SLOT).value = newBeacon;\n    }\n\n    /**\n     * @dev Perform beacon upgrade with additional setup call. Note: This upgrades the address of the beacon, it does\n     * not upgrade the implementation contained in the beacon (see {UpgradeableBeacon-_setImplementation} for that).\n     *\n     * Emits a {BeaconUpgraded} event.\n     */\n    function _upgradeBeaconToAndCall(address newBeacon, bytes memory data, bool forceCall) internal {\n        _setBeacon(newBeacon);\n        emit BeaconUpgraded(newBeacon);\n        if (data.length > 0 || forceCall) {\n            AddressUpgradeable.functionDelegateCall(IBeaconUpgradeable(newBeacon).implementation(), data);\n        }\n    }\n\n    /**\n     * @dev This empty reserved space is put in place to allow future versions to add new\n     * variables without shifting down storage in the inheritance chain.\n     * See https://docs.openzeppelin.com/contracts/4.x/upgradeable#storage_gaps\n     */\n    uint256[50] private __gap;\n}\n"
    },
    "@openzeppelin/contracts-upgradeable/proxy/beacon/IBeaconUpgradeable.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (proxy/beacon/IBeacon.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev This is the interface that {BeaconProxy} expects of its beacon.\n */\ninterface IBeaconUpgradeable {\n    /**\n     * @dev Must return an address that can be used as a delegate call target.\n     *\n     * {BeaconProxy} will check that this address is a contract.\n     */\n    function implementation() external view returns (address);\n}\n"
    },
    "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.9.0) (proxy/utils/Initializable.sol)\n\npragma solidity ^0.8.2;\n\nimport \"../../utils/AddressUpgradeable.sol\";\n\n/**\n * @dev This is a base contract to aid in writing upgradeable contracts, or any kind of contract that will be deployed\n * behind a proxy. Since proxied contracts do not make use of a constructor, it's common to move constructor logic to an\n * external initializer function, usually called `initialize`. It then becomes necessary to protect this initializer\n * function so it can only be called once. The {initializer} modifier provided by this contract will have this effect.\n *\n * The initialization functions use a version number. Once a version number is used, it is consumed and cannot be\n * reused. This mechanism prevents re-execution of each \"step\" but allows the creation of new initialization steps in\n * case an upgrade adds a module that needs to be initialized.\n *\n * For example:\n *\n * [.hljs-theme-light.nopadding]\n * ```solidity\n * contract MyToken is ERC20Upgradeable {\n *     function initialize() initializer public {\n *         __ERC20_init(\"MyToken\", \"MTK\");\n *     }\n * }\n *\n * contract MyTokenV2 is MyToken, ERC20PermitUpgradeable {\n *     function initializeV2() reinitializer(2) public {\n *         __ERC20Permit_init(\"MyToken\");\n *     }\n * }\n * ```\n *\n * TIP: To avoid leaving the proxy in an uninitialized state, the initializer function should be called as early as\n * possible by providing the encoded function call as the `_data` argument to {ERC1967Proxy-constructor}.\n *\n * CAUTION: When used with inheritance, manual care must be taken to not invoke a parent initializer twice, or to ensure\n * that all initializers are idempotent. This is not verified automatically as constructors are by Solidity.\n *\n * [CAUTION]\n * ====\n * Avoid leaving a contract uninitialized.\n *\n * An uninitialized contract can be taken over by an attacker. This applies to both a proxy and its implementation\n * contract, which may impact the proxy. To prevent the implementation contract from being used, you should invoke\n * the {_disableInitializers} function in the constructor to automatically lock it when it is deployed:\n *\n * [.hljs-theme-light.nopadding]\n * ```\n * /// @custom:oz-upgrades-unsafe-allow constructor\n * constructor() {\n *     _disableInitializers();\n * }\n * ```\n * ====\n */\nabstract contract Initializable {\n    /**\n     * @dev Indicates that the contract has been initialized.\n     * @custom:oz-retyped-from bool\n     */\n    uint8 private _initialized;\n\n    /**\n     * @dev Indicates that the contract is in the process of being initialized.\n     */\n    bool private _initializing;\n\n    /**\n     * @dev Triggered when the contract has been initialized or reinitialized.\n     */\n    event Initialized(uint8 version);\n\n    /**\n     * @dev A modifier that defines a protected initializer function that can be invoked at most once. In its scope,\n     * `onlyInitializing` functions can be used to initialize parent contracts.\n     *\n     * Similar to `reinitializer(1)`, except that functions marked with `initializer` can be nested in the context of a\n     * constructor.\n     *\n     * Emits an {Initialized} event.\n     */\n    modifier initializer() {\n        bool isTopLevelCall = !_initializing;\n        require(\n            (isTopLevelCall && _initialized < 1) || (!AddressUpgradeable.isContract(address(this)) && _initialized == 1),\n            \"Initializable: contract is already initialized\"\n        );\n        _initialized = 1;\n        if (isTopLevelCall) {\n            _initializing = true;\n        }\n        _;\n        if (isTopLevelCall) {\n            _initializing = false;\n            emit Initialized(1);\n        }\n    }\n\n    /**\n     * @dev A modifier that defines a protected reinitializer function that can be invoked at most once, and only if the\n     * contract hasn't been initialized to a greater version before. In its scope, `onlyInitializing` functions can be\n     * used to initialize parent contracts.\n     *\n     * A reinitializer may be used after the original initialization step. This is essential to configure modules that\n     * are added through upgrades and that require initialization.\n     *\n     * When `version` is 1, this modifier is similar to `initializer`, except that functions marked with `reinitializer`\n     * cannot be nested. If one is invoked in the context of another, execution will revert.\n     *\n     * Note that versions can jump in increments greater than 1; this implies that if multiple reinitializers coexist in\n     * a contract, executing them in the right order is up to the developer or operator.\n     *\n     * WARNING: setting the version to 255 will prevent any future reinitialization.\n     *\n     * Emits an {Initialized} event.\n     */\n    modifier reinitializer(uint8 version) {\n        require(!_initializing && _initialized < version, \"Initializable: contract is already initialized\");\n        _initialized = version;\n        _initializing = true;\n        _;\n        _initializing = false;\n        emit Initialized(version);\n    }\n\n    /**\n     * @dev Modifier to protect an initialization function so that it can only be invoked by functions with the\n     * {initializer} and {reinitializer} modifiers, directly or indirectly.\n     */\n    modifier onlyInitializing() {\n        require(_initializing, \"Initializable: contract is not initializing\");\n        _;\n    }\n\n    /**\n     * @dev Locks the contract, preventing any future reinitialization. This cannot be part of an initializer call.\n     * Calling this in the constructor of a contract will prevent that contract from being initialized or reinitialized\n     * to any version. It is recommended to use this to lock implementation contracts that are designed to be called\n     * through proxies.\n     *\n     * Emits an {Initialized} event the first time it is successfully executed.\n     */\n    function _disableInitializers() internal virtual {\n        require(!_initializing, \"Initializable: contract is initializing\");\n        if (_initialized != type(uint8).max) {\n            _initialized = type(uint8).max;\n            emit Initialized(type(uint8).max);\n        }\n    }\n\n    /**\n     * @dev Returns the highest version that has been initialized. See {reinitializer}.\n     */\n    function _getInitializedVersion() internal view returns (uint8) {\n        return _initialized;\n    }\n\n    /**\n     * @dev Returns `true` if the contract is currently initializing. See {onlyInitializing}.\n     */\n    function _isInitializing() internal view returns (bool) {\n        return _initializing;\n    }\n}\n"
    },
    "@openzeppelin/contracts-upgradeable/proxy/utils/UUPSUpgradeable.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.9.0) (proxy/utils/UUPSUpgradeable.sol)\n\npragma solidity ^0.8.0;\n\nimport \"../../interfaces/draft-IERC1822Upgradeable.sol\";\nimport \"../ERC1967/ERC1967UpgradeUpgradeable.sol\";\nimport \"./Initializable.sol\";\n\n/**\n * @dev An upgradeability mechanism designed for UUPS proxies. The functions included here can perform an upgrade of an\n * {ERC1967Proxy}, when this contract is set as the implementation behind such a proxy.\n *\n * A security mechanism ensures that an upgrade does not turn off upgradeability accidentally, although this risk is\n * reinstated if the upgrade retains upgradeability but removes the security mechanism, e.g. by replacing\n * `UUPSUpgradeable` with a custom implementation of upgrades.\n *\n * The {_authorizeUpgrade} function must be overridden to include access restriction to the upgrade mechanism.\n *\n * _Available since v4.1._\n */\nabstract contract UUPSUpgradeable is Initializable, IERC1822ProxiableUpgradeable, ERC1967UpgradeUpgradeable {\n    function __UUPSUpgradeable_init() internal onlyInitializing {\n    }\n\n    function __UUPSUpgradeable_init_unchained() internal onlyInitializing {\n    }\n    /// @custom:oz-upgrades-unsafe-allow state-variable-immutable state-variable-assignment\n    address private immutable __self = address(this);\n\n    /**\n     * @dev Check that the execution is being performed through a delegatecall call and that the execution context is\n     * a proxy contract with an implementation (as defined in ERC1967) pointing to self. This should only be the case\n     * for UUPS and transparent proxies that are using the current contract as their implementation. Execution of a\n     * function through ERC1167 minimal proxies (clones) would not normally pass this test, but is not guaranteed to\n     * fail.\n     */\n    modifier onlyProxy() {\n        require(address(this) != __self, \"Function must be called through delegatecall\");\n        require(_getImplementation() == __self, \"Function must be called through active proxy\");\n        _;\n    }\n\n    /**\n     * @dev Check that the execution is not being performed through a delegate call. This allows a function to be\n     * callable on the implementing contract but not through proxies.\n     */\n    modifier notDelegated() {\n        require(address(this) == __self, \"UUPSUpgradeable: must not be called through delegatecall\");\n        _;\n    }\n\n    /**\n     * @dev Implementation of the ERC1822 {proxiableUUID} function. This returns the storage slot used by the\n     * implementation. It is used to validate the implementation's compatibility when performing an upgrade.\n     *\n     * IMPORTANT: A proxy pointing at a proxiable contract should not be considered proxiable itself, because this risks\n     * bricking a proxy that upgrades to it, by delegating to itself until out of gas. Thus it is critical that this\n     * function revert if invoked through a proxy. This is guaranteed by the `notDelegated` modifier.\n     */\n    function proxiableUUID() external view virtual override notDelegated returns (bytes32) {\n        return _IMPLEMENTATION_SLOT;\n    }\n\n    /**\n     * @dev Upgrade the implementation of the proxy to `newImplementation`.\n     *\n     * Calls {_authorizeUpgrade}.\n     *\n     * Emits an {Upgraded} event.\n     *\n     * @custom:oz-upgrades-unsafe-allow-reachable delegatecall\n     */\n    function upgradeTo(address newImplementation) public virtual onlyProxy {\n        _authorizeUpgrade(newImplementation);\n        _upgradeToAndCallUUPS(newImplementation, new bytes(0), false);\n    }\n\n    /**\n     * @dev Upgrade the implementation of the proxy to `newImplementation`, and subsequently execute the function call\n     * encoded in `data`.\n     *\n     * Calls {_authorizeUpgrade}.\n     *\n     * Emits an {Upgraded} event.\n     *\n     * @custom:oz-upgrades-unsafe-allow-reachable delegatecall\n     */\n    function upgradeToAndCall(address newImplementation, bytes memory data) public payable virtual onlyProxy {\n        _authorizeUpgrade(newImplementation);\n        _upgradeToAndCallUUPS(newImplementation, data, true);\n    }\n\n    /**\n     * @dev Function that should revert when `msg.sender` is not authorized to upgrade the contract. Called by\n     * {upgradeTo} and {upgradeToAndCall}.\n     *\n     * Normally, this function will use an xref:access.adoc[access control] modifier such as {Ownable-onlyOwner}.\n     *\n     * ```solidity\n     * function _authorizeUpgrade(address) internal override onlyOwner {}\n     * ```\n     */\n    function _authorizeUpgrade(address newImplementation) internal virtual;\n\n    /**\n     * @dev This empty reserved space is put in place to allow future versions to add new\n     * variables without shifting down storage in the inheritance chain.\n     * See https://docs.openzeppelin.com/contracts/4.x/upgradeable#storage_gaps\n     */\n    uint256[50] private __gap;\n}\n"
    },
    "@openzeppelin/contracts-upgradeable/utils/AddressUpgradeable.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.9.0) (utils/Address.sol)\n\npragma solidity ^0.8.1;\n\n/**\n * @dev Collection of functions related to the address type\n */\nlibrary AddressUpgradeable {\n    /**\n     * @dev Returns true if `account` is a contract.\n     *\n     * [IMPORTANT]\n     * ====\n     * It is unsafe to assume that an address for which this function returns\n     * false is an externally-owned account (EOA) and not a contract.\n     *\n     * Among others, `isContract` will return false for the following\n     * types of addresses:\n     *\n     *  - an externally-owned account\n     *  - a contract in construction\n     *  - an address where a contract will be created\n     *  - an address where a contract lived, but was destroyed\n     *\n     * Furthermore, `isContract` will also return true if the target contract within\n     * the same transaction is already scheduled for destruction by `SELFDESTRUCT`,\n     * which only has an effect at the end of a transaction.\n     * ====\n     *\n     * [IMPORTANT]\n     * ====\n     * You shouldn't rely on `isContract` to protect against flash loan attacks!\n     *\n     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets\n     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract\n     * constructor.\n     * ====\n     */\n    function isContract(address account) internal view returns (bool) {\n        // This method relies on extcodesize/address.code.length, which returns 0\n        // for contracts in construction, since the code is only stored at the end\n        // of the constructor execution.\n\n        return account.code.length > 0;\n    }\n\n    /**\n     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to\n     * `recipient`, forwarding all available gas and reverting on errors.\n     *\n     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost\n     * of certain opcodes, possibly making contracts go over the 2300 gas limit\n     * imposed by `transfer`, making them unable to receive funds via\n     * `transfer`. {sendValue} removes this limitation.\n     *\n     * https://consensys.net/diligence/blog/2019/09/stop-using-soliditys-transfer-now/[Learn more].\n     *\n     * IMPORTANT: because control is transferred to `recipient`, care must be\n     * taken to not create reentrancy vulnerabilities. Consider using\n     * {ReentrancyGuard} or the\n     * https://solidity.readthedocs.io/en/v0.8.0/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].\n     */\n    function sendValue(address payable recipient, uint256 amount) internal {\n        require(address(this).balance >= amount, \"Address: insufficient balance\");\n\n        (bool success, ) = recipient.call{value: amount}(\"\");\n        require(success, \"Address: unable to send value, recipient may have reverted\");\n    }\n\n    /**\n     * @dev Performs a Solidity function call using a low level `call`. A\n     * plain `call` is an unsafe replacement for a function call: use this\n     * function instead.\n     *\n     * If `target` reverts with a revert reason, it is bubbled up by this\n     * function (like regular Solidity function calls).\n     *\n     * Returns the raw returned data. To convert to the expected return value,\n     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].\n     *\n     * Requirements:\n     *\n     * - `target` must be a contract.\n     * - calling `target` with `data` must not revert.\n     *\n     * _Available since v3.1._\n     */\n    function functionCall(address target, bytes memory data) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, 0, \"Address: low-level call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with\n     * `errorMessage` as a fallback revert reason when `target` reverts.\n     *\n     * _Available since v3.1._\n     */\n    function functionCall(\n        address target,\n        bytes memory data,\n        string memory errorMessage\n    ) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, 0, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but also transferring `value` wei to `target`.\n     *\n     * Requirements:\n     *\n     * - the calling contract must have an ETH balance of at least `value`.\n     * - the called Solidity function must be `payable`.\n     *\n     * _Available since v3.1._\n     */\n    function functionCallWithValue(address target, bytes memory data, uint256 value) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, value, \"Address: low-level call with value failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but\n     * with `errorMessage` as a fallback revert reason when `target` reverts.\n     *\n     * _Available since v3.1._\n     */\n    function functionCallWithValue(\n        address target,\n        bytes memory data,\n        uint256 value,\n        string memory errorMessage\n    ) internal returns (bytes memory) {\n        require(address(this).balance >= value, \"Address: insufficient balance for call\");\n        (bool success, bytes memory returndata) = target.call{value: value}(data);\n        return verifyCallResultFromTarget(target, success, returndata, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but performing a static call.\n     *\n     * _Available since v3.3._\n     */\n    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {\n        return functionStaticCall(target, data, \"Address: low-level static call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],\n     * but performing a static call.\n     *\n     * _Available since v3.3._\n     */\n    function functionStaticCall(\n        address target,\n        bytes memory data,\n        string memory errorMessage\n    ) internal view returns (bytes memory) {\n        (bool success, bytes memory returndata) = target.staticcall(data);\n        return verifyCallResultFromTarget(target, success, returndata, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but performing a delegate call.\n     *\n     * _Available since v3.4._\n     */\n    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {\n        return functionDelegateCall(target, data, \"Address: low-level delegate call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],\n     * but performing a delegate call.\n     *\n     * _Available since v3.4._\n     */\n    function functionDelegateCall(\n        address target,\n        bytes memory data,\n        string memory errorMessage\n    ) internal returns (bytes memory) {\n        (bool success, bytes memory returndata) = target.delegatecall(data);\n        return verifyCallResultFromTarget(target, success, returndata, errorMessage);\n    }\n\n    /**\n     * @dev Tool to verify that a low level call to smart-contract was successful, and revert (either by bubbling\n     * the revert reason or using the provided one) in case of unsuccessful call or if target was not a contract.\n     *\n     * _Available since v4.8._\n     */\n    function verifyCallResultFromTarget(\n        address target,\n        bool success,\n        bytes memory returndata,\n        string memory errorMessage\n    ) internal view returns (bytes memory) {\n        if (success) {\n            if (returndata.length == 0) {\n                // only check isContract if the call was successful and the return data is empty\n                // otherwise we already know that it was a contract\n                require(isContract(target), \"Address: call to non-contract\");\n            }\n            return returndata;\n        } else {\n            _revert(returndata, errorMessage);\n        }\n    }\n\n    /**\n     * @dev Tool to verify that a low level call was successful, and revert if it wasn't, either by bubbling the\n     * revert reason or using the provided one.\n     *\n     * _Available since v4.3._\n     */\n    function verifyCallResult(\n        bool success,\n        bytes memory returndata,\n        string memory errorMessage\n    ) internal pure returns (bytes memory) {\n        if (success) {\n            return returndata;\n        } else {\n            _revert(returndata, errorMessage);\n        }\n    }\n\n    function _revert(bytes memory returndata, string memory errorMessage) private pure {\n        // Look for revert reason and bubble it up if present\n        if (returndata.length > 0) {\n            // The easiest way to bubble the revert reason is using memory via assembly\n            /// @solidity memory-safe-assembly\n            assembly {\n                let returndata_size := mload(returndata)\n                revert(add(32, returndata), returndata_size)\n            }\n        } else {\n            revert(errorMessage);\n        }\n    }\n}\n"
    },
    "@openzeppelin/contracts-upgradeable/utils/StorageSlotUpgradeable.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.9.0) (utils/StorageSlot.sol)\n// This file was procedurally generated from scripts/generate/templates/StorageSlot.js.\n\npragma solidity ^0.8.0;\n\n/**\n * @dev Library for reading and writing primitive types to specific storage slots.\n *\n * Storage slots are often used to avoid storage conflict when dealing with upgradeable contracts.\n * This library helps with reading and writing to such slots without the need for inline assembly.\n *\n * The functions in this library return Slot structs that contain a `value` member that can be used to read or write.\n *\n * Example usage to set ERC1967 implementation slot:\n * ```solidity\n * contract ERC1967 {\n *     bytes32 internal constant _IMPLEMENTATION_SLOT = 0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc;\n *\n *     function _getImplementation() internal view returns (address) {\n *         return StorageSlot.getAddressSlot(_IMPLEMENTATION_SLOT).value;\n *     }\n *\n *     function _setImplementation(address newImplementation) internal {\n *         require(Address.isContract(newImplementation), \"ERC1967: new implementation is not a contract\");\n *         StorageSlot.getAddressSlot(_IMPLEMENTATION_SLOT).value = newImplementation;\n *     }\n * }\n * ```\n *\n * _Available since v4.1 for `address`, `bool`, `bytes32`, `uint256`._\n * _Available since v4.9 for `string`, `bytes`._\n */\nlibrary StorageSlotUpgradeable {\n    struct AddressSlot {\n        address value;\n    }\n\n    struct BooleanSlot {\n        bool value;\n    }\n\n    struct Bytes32Slot {\n        bytes32 value;\n    }\n\n    struct Uint256Slot {\n        uint256 value;\n    }\n\n    struct StringSlot {\n        string value;\n    }\n\n    struct BytesSlot {\n        bytes value;\n    }\n\n    /**\n     * @dev Returns an `AddressSlot` with member `value` located at `slot`.\n     */\n    function getAddressSlot(bytes32 slot) internal pure returns (AddressSlot storage r) {\n        /// @solidity memory-safe-assembly\n        assembly {\n            r.slot := slot\n        }\n    }\n\n    /**\n     * @dev Returns an `BooleanSlot` with member `value` located at `slot`.\n     */\n    function getBooleanSlot(bytes32 slot) internal pure returns (BooleanSlot storage r) {\n        /// @solidity memory-safe-assembly\n        assembly {\n            r.slot := slot\n        }\n    }\n\n    /**\n     * @dev Returns an `Bytes32Slot` with member `value` located at `slot`.\n     */\n    function getBytes32Slot(bytes32 slot) internal pure returns (Bytes32Slot storage r) {\n        /// @solidity memory-safe-assembly\n        assembly {\n            r.slot := slot\n        }\n    }\n\n    /**\n     * @dev Returns an `Uint256Slot` with member `value` located at `slot`.\n     */\n    function getUint256Slot(bytes32 slot) internal pure returns (Uint256Slot storage r) {\n        /// @solidity memory-safe-assembly\n        assembly {\n            r.slot := slot\n        }\n    }\n\n    /**\n     * @dev Returns an `StringSlot` with member `value` located at `slot`.\n     */\n    function getStringSlot(bytes32 slot) internal pure returns (StringSlot storage r) {\n        /// @solidity memory-safe-assembly\n        assembly {\n            r.slot := slot\n        }\n    }\n\n    /**\n     * @dev Returns an `StringSlot` representation of the string storage pointer `store`.\n     */\n    function getStringSlot(string storage store) internal pure returns (StringSlot storage r) {\n        /// @solidity memory-safe-assembly\n        assembly {\n            r.slot := store.slot\n        }\n    }\n\n    /**\n     * @dev Returns an `BytesSlot` with member `value` located at `slot`.\n     */\n    function getBytesSlot(bytes32 slot) internal pure returns (BytesSlot storage r) {\n        /// @solidity memory-safe-assembly\n        assembly {\n            r.slot := slot\n        }\n    }\n\n    /**\n     * @dev Returns an `BytesSlot` representation of the bytes storage pointer `store`.\n     */\n    function getBytesSlot(bytes storage store) internal pure returns (BytesSlot storage r) {\n        /// @solidity memory-safe-assembly\n        assembly {\n            r.slot := store.slot\n        }\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/Address.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v5.0.0) (utils/Address.sol)\n\npragma solidity ^0.8.20;\n\n/**\n * @dev Collection of functions related to the address type\n */\nlibrary Address {\n    /**\n     * @dev The ETH balance of the account is not enough to perform the operation.\n     */\n    error AddressInsufficientBalance(address account);\n\n    /**\n     * @dev There's no code at `target` (it is not a contract).\n     */\n    error AddressEmptyCode(address target);\n\n    /**\n     * @dev A call to an address target failed. The target may have reverted.\n     */\n    error FailedInnerCall();\n\n    /**\n     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to\n     * `recipient`, forwarding all available gas and reverting on errors.\n     *\n     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost\n     * of certain opcodes, possibly making contracts go over the 2300 gas limit\n     * imposed by `transfer`, making them unable to receive funds via\n     * `transfer`. {sendValue} removes this limitation.\n     *\n     * https://consensys.net/diligence/blog/2019/09/stop-using-soliditys-transfer-now/[Learn more].\n     *\n     * IMPORTANT: because control is transferred to `recipient`, care must be\n     * taken to not create reentrancy vulnerabilities. Consider using\n     * {ReentrancyGuard} or the\n     * https://solidity.readthedocs.io/en/v0.8.20/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].\n     */\n    function sendValue(address payable recipient, uint256 amount) internal {\n        if (address(this).balance < amount) {\n            revert AddressInsufficientBalance(address(this));\n        }\n\n        (bool success, ) = recipient.call{value: amount}(\"\");\n        if (!success) {\n            revert FailedInnerCall();\n        }\n    }\n\n    /**\n     * @dev Performs a Solidity function call using a low level `call`. A\n     * plain `call` is an unsafe replacement for a function call: use this\n     * function instead.\n     *\n     * If `target` reverts with a revert reason or custom error, it is bubbled\n     * up by this function (like regular Solidity function calls). However, if\n     * the call reverted with no returned reason, this function reverts with a\n     * {FailedInnerCall} error.\n     *\n     * Returns the raw returned data. To convert to the expected return value,\n     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].\n     *\n     * Requirements:\n     *\n     * - `target` must be a contract.\n     * - calling `target` with `data` must not revert.\n     */\n    function functionCall(address target, bytes memory data) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, 0);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but also transferring `value` wei to `target`.\n     *\n     * Requirements:\n     *\n     * - the calling contract must have an ETH balance of at least `value`.\n     * - the called Solidity function must be `payable`.\n     */\n    function functionCallWithValue(address target, bytes memory data, uint256 value) internal returns (bytes memory) {\n        if (address(this).balance < value) {\n            revert AddressInsufficientBalance(address(this));\n        }\n        (bool success, bytes memory returndata) = target.call{value: value}(data);\n        return verifyCallResultFromTarget(target, success, returndata);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but performing a static call.\n     */\n    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {\n        (bool success, bytes memory returndata) = target.staticcall(data);\n        return verifyCallResultFromTarget(target, success, returndata);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but performing a delegate call.\n     */\n    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {\n        (bool success, bytes memory returndata) = target.delegatecall(data);\n        return verifyCallResultFromTarget(target, success, returndata);\n    }\n\n    /**\n     * @dev Tool to verify that a low level call to smart-contract was successful, and reverts if the target\n     * was not a contract or bubbling up the revert reason (falling back to {FailedInnerCall}) in case of an\n     * unsuccessful call.\n     */\n    function verifyCallResultFromTarget(\n        address target,\n        bool success,\n        bytes memory returndata\n    ) internal view returns (bytes memory) {\n        if (!success) {\n            _revert(returndata);\n        } else {\n            // only check if target is a contract if the call was successful and the return data is empty\n            // otherwise we already know that it was a contract\n            if (returndata.length == 0 && target.code.length == 0) {\n                revert AddressEmptyCode(target);\n            }\n            return returndata;\n        }\n    }\n\n    /**\n     * @dev Tool to verify that a low level call was successful, and reverts if it wasn't, either by bubbling the\n     * revert reason or with a default {FailedInnerCall} error.\n     */\n    function verifyCallResult(bool success, bytes memory returndata) internal pure returns (bytes memory) {\n        if (!success) {\n            _revert(returndata);\n        } else {\n            return returndata;\n        }\n    }\n\n    /**\n     * @dev Reverts with returndata if present. Otherwise reverts with {FailedInnerCall}.\n     */\n    function _revert(bytes memory returndata) private pure {\n        // Look for revert reason and bubble it up if present\n        if (returndata.length > 0) {\n            // The easiest way to bubble the revert reason is using memory via assembly\n            /// @solidity memory-safe-assembly\n            assembly {\n                let returndata_size := mload(returndata)\n                revert(add(32, returndata), returndata_size)\n            }\n        } else {\n            revert FailedInnerCall();\n        }\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/cryptography/MerkleProof.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v5.0.0) (utils/cryptography/MerkleProof.sol)\n\npragma solidity ^0.8.20;\n\n/**\n * @dev These functions deal with verification of Merkle Tree proofs.\n *\n * The tree and the proofs can be generated using our\n * https://github.com/OpenZeppelin/merkle-tree[JavaScript library].\n * You will find a quickstart guide in the readme.\n *\n * WARNING: You should avoid using leaf values that are 64 bytes long prior to\n * hashing, or use a hash function other than keccak256 for hashing leaves.\n * This is because the concatenation of a sorted pair of internal nodes in\n * the Merkle tree could be reinterpreted as a leaf value.\n * OpenZeppelin's JavaScript library generates Merkle trees that are safe\n * against this attack out of the box.\n */\nlibrary MerkleProof {\n    /**\n     *@dev The multiproof provided is not valid.\n     */\n    error MerkleProofInvalidMultiproof();\n\n    /**\n     * @dev Returns true if a `leaf` can be proved to be a part of a Merkle tree\n     * defined by `root`. For this, a `proof` must be provided, containing\n     * sibling hashes on the branch from the leaf to the root of the tree. Each\n     * pair of leaves and each pair of pre-images are assumed to be sorted.\n     */\n    function verify(bytes32[] memory proof, bytes32 root, bytes32 leaf) internal pure returns (bool) {\n        return processProof(proof, leaf) == root;\n    }\n\n    /**\n     * @dev Calldata version of {verify}\n     */\n    function verifyCalldata(bytes32[] calldata proof, bytes32 root, bytes32 leaf) internal pure returns (bool) {\n        return processProofCalldata(proof, leaf) == root;\n    }\n\n    /**\n     * @dev Returns the rebuilt hash obtained by traversing a Merkle tree up\n     * from `leaf` using `proof`. A `proof` is valid if and only if the rebuilt\n     * hash matches the root of the tree. When processing the proof, the pairs\n     * of leafs & pre-images are assumed to be sorted.\n     */\n    function processProof(bytes32[] memory proof, bytes32 leaf) internal pure returns (bytes32) {\n        bytes32 computedHash = leaf;\n        for (uint256 i = 0; i < proof.length; i++) {\n            computedHash = _hashPair(computedHash, proof[i]);\n        }\n        return computedHash;\n    }\n\n    /**\n     * @dev Calldata version of {processProof}\n     */\n    function processProofCalldata(bytes32[] calldata proof, bytes32 leaf) internal pure returns (bytes32) {\n        bytes32 computedHash = leaf;\n        for (uint256 i = 0; i < proof.length; i++) {\n            computedHash = _hashPair(computedHash, proof[i]);\n        }\n        return computedHash;\n    }\n\n    /**\n     * @dev Returns true if the `leaves` can be simultaneously proven to be a part of a Merkle tree defined by\n     * `root`, according to `proof` and `proofFlags` as described in {processMultiProof}.\n     *\n     * CAUTION: Not all Merkle trees admit multiproofs. See {processMultiProof} for details.\n     */\n    function multiProofVerify(\n        bytes32[] memory proof,\n        bool[] memory proofFlags,\n        bytes32 root,\n        bytes32[] memory leaves\n    ) internal pure returns (bool) {\n        return processMultiProof(proof, proofFlags, leaves) == root;\n    }\n\n    /**\n     * @dev Calldata version of {multiProofVerify}\n     *\n     * CAUTION: Not all Merkle trees admit multiproofs. See {processMultiProof} for details.\n     */\n    function multiProofVerifyCalldata(\n        bytes32[] calldata proof,\n        bool[] calldata proofFlags,\n        bytes32 root,\n        bytes32[] memory leaves\n    ) internal pure returns (bool) {\n        return processMultiProofCalldata(proof, proofFlags, leaves) == root;\n    }\n\n    /**\n     * @dev Returns the root of a tree reconstructed from `leaves` and sibling nodes in `proof`. The reconstruction\n     * proceeds by incrementally reconstructing all inner nodes by combining a leaf/inner node with either another\n     * leaf/inner node or a proof sibling node, depending on whether each `proofFlags` item is true or false\n     * respectively.\n     *\n     * CAUTION: Not all Merkle trees admit multiproofs. To use multiproofs, it is sufficient to ensure that: 1) the tree\n     * is complete (but not necessarily perfect), 2) the leaves to be proven are in the opposite order they are in the\n     * tree (i.e., as seen from right to left starting at the deepest layer and continuing at the next layer).\n     */\n    function processMultiProof(\n        bytes32[] memory proof,\n        bool[] memory proofFlags,\n        bytes32[] memory leaves\n    ) internal pure returns (bytes32 merkleRoot) {\n        // This function rebuilds the root hash by traversing the tree up from the leaves. The root is rebuilt by\n        // consuming and producing values on a queue. The queue starts with the `leaves` array, then goes onto the\n        // `hashes` array. At the end of the process, the last hash in the `hashes` array should contain the root of\n        // the Merkle tree.\n        uint256 leavesLen = leaves.length;\n        uint256 proofLen = proof.length;\n        uint256 totalHashes = proofFlags.length;\n\n        // Check proof validity.\n        if (leavesLen + proofLen != totalHashes + 1) {\n            revert MerkleProofInvalidMultiproof();\n        }\n\n        // The xxxPos values are \"pointers\" to the next value to consume in each array. All accesses are done using\n        // `xxx[xxxPos++]`, which return the current value and increment the pointer, thus mimicking a queue's \"pop\".\n        bytes32[] memory hashes = new bytes32[](totalHashes);\n        uint256 leafPos = 0;\n        uint256 hashPos = 0;\n        uint256 proofPos = 0;\n        // At each step, we compute the next hash using two values:\n        // - a value from the \"main queue\". If not all leaves have been consumed, we get the next leaf, otherwise we\n        //   get the next hash.\n        // - depending on the flag, either another value from the \"main queue\" (merging branches) or an element from the\n        //   `proof` array.\n        for (uint256 i = 0; i < totalHashes; i++) {\n            bytes32 a = leafPos < leavesLen ? leaves[leafPos++] : hashes[hashPos++];\n            bytes32 b = proofFlags[i]\n                ? (leafPos < leavesLen ? leaves[leafPos++] : hashes[hashPos++])\n                : proof[proofPos++];\n            hashes[i] = _hashPair(a, b);\n        }\n\n        if (totalHashes > 0) {\n            if (proofPos != proofLen) {\n                revert MerkleProofInvalidMultiproof();\n            }\n            unchecked {\n                return hashes[totalHashes - 1];\n            }\n        } else if (leavesLen > 0) {\n            return leaves[0];\n        } else {\n            return proof[0];\n        }\n    }\n\n    /**\n     * @dev Calldata version of {processMultiProof}.\n     *\n     * CAUTION: Not all Merkle trees admit multiproofs. See {processMultiProof} for details.\n     */\n    function processMultiProofCalldata(\n        bytes32[] calldata proof,\n        bool[] calldata proofFlags,\n        bytes32[] memory leaves\n    ) internal pure returns (bytes32 merkleRoot) {\n        // This function rebuilds the root hash by traversing the tree up from the leaves. The root is rebuilt by\n        // consuming and producing values on a queue. The queue starts with the `leaves` array, then goes onto the\n        // `hashes` array. At the end of the process, the last hash in the `hashes` array should contain the root of\n        // the Merkle tree.\n        uint256 leavesLen = leaves.length;\n        uint256 proofLen = proof.length;\n        uint256 totalHashes = proofFlags.length;\n\n        // Check proof validity.\n        if (leavesLen + proofLen != totalHashes + 1) {\n            revert MerkleProofInvalidMultiproof();\n        }\n\n        // The xxxPos values are \"pointers\" to the next value to consume in each array. All accesses are done using\n        // `xxx[xxxPos++]`, which return the current value and increment the pointer, thus mimicking a queue's \"pop\".\n        bytes32[] memory hashes = new bytes32[](totalHashes);\n        uint256 leafPos = 0;\n        uint256 hashPos = 0;\n        uint256 proofPos = 0;\n        // At each step, we compute the next hash using two values:\n        // - a value from the \"main queue\". If not all leaves have been consumed, we get the next leaf, otherwise we\n        //   get the next hash.\n        // - depending on the flag, either another value from the \"main queue\" (merging branches) or an element from the\n        //   `proof` array.\n        for (uint256 i = 0; i < totalHashes; i++) {\n            bytes32 a = leafPos < leavesLen ? leaves[leafPos++] : hashes[hashPos++];\n            bytes32 b = proofFlags[i]\n                ? (leafPos < leavesLen ? leaves[leafPos++] : hashes[hashPos++])\n                : proof[proofPos++];\n            hashes[i] = _hashPair(a, b);\n        }\n\n        if (totalHashes > 0) {\n            if (proofPos != proofLen) {\n                revert MerkleProofInvalidMultiproof();\n            }\n            unchecked {\n                return hashes[totalHashes - 1];\n            }\n        } else if (leavesLen > 0) {\n            return leaves[0];\n        } else {\n            return proof[0];\n        }\n    }\n\n    /**\n     * @dev Sorts the pair (a, b) and hashes the result.\n     */\n    function _hashPair(bytes32 a, bytes32 b) private pure returns (bytes32) {\n        return a < b ? _efficientHash(a, b) : _efficientHash(b, a);\n    }\n\n    /**\n     * @dev Implementation of keccak256(abi.encode(a, b)) that doesn't allocate or expand memory.\n     */\n    function _efficientHash(bytes32 a, bytes32 b) private pure returns (bytes32 value) {\n        /// @solidity memory-safe-assembly\n        assembly {\n            mstore(0x00, a)\n            mstore(0x20, b)\n            value := keccak256(0x00, 0x40)\n        }\n    }\n}\n"
    },
    "contracts/protocol/es/EmissionSchedule.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.4;\n\n/**\n * @title Emission Schedule\n *\n * @notice Emission schedule defines the rate at which new tokens are released into circulation over time.\n *      It's a function that outlines the number of tokens released per unit of time,\n *      such as during a month or year. \n *\n * @notice Emission schedule defines the emission volume of the new tokens released over time.\n *      It's a function that outlines the number of tokens released so far, by the given time.\n *      See emissionVolume(t) function\n *\n * @notice Emission rate is the derivative of the emission volume with respect to time,\n *      and emission volume is an integral of the emission rate over time.\n *      See emissionRate(t, dt) function\n * \n * @notice Emission schedule is defined on the time interval [start time, start time + duration],\n *      when token emission happens; also called an `emission period`.\n *      See emissionPeriod() function\n *\n * @notice Emission schedule defines initial (often zero) and final emission volumes.\n *      See initialEmissionVolume() and totalEmissionVolume() functions\n *\n * @notice The source of the new tokens released into circulation can be anything:\n *      tokens may get created (minted), transferred from the treasury wallet, etc.\n *      The source of the tokens is implementation dependent.\n *\n * @dev This interface aims to be generic and allow various types of implementations with\n *      various shapes of the emission curves;\n *      still the interface is specific enough for the clients app to integrate with.\n *\n * @dev Implementations MUST guarantee the following invariants ALWAYS hold TRUE:\n *      emissionVolume(t = start time) = initialEmissionVolume()\n *      emissionVolume(t < start time) = 0\n *      emissionVolume(t = start time + duration) = totalEmissionVolume()\n *      emissionVolume(t > start time + duration) = totalEmissionVolume()\n *      emissionVolume(t) - emissionVolume(t - dt) = emissionRate(t, dt)\n *      emissionVolume(t1) - emissionVolume(t0) = emissionVolumeDelta(t0, t1)\n *      emissionVolumeDelta(t1, t2) = 0, if t1 = t2\n *      alreadyReleasedAmount() <= emissionVolume(t = now)\n *      toBeReleasedAmount() = totalEmissionVolume() - alreadyReleasedAmount()\n *      canReleaseNowAmount() = emissionVolume(t = now) - alreadyReleasedAmount()\n *\n * @dev Implementation may not support any `dt` value in emissionRate(t, dt),\n *      but it must support at least dt = 1\n *\n * @author Lizard Labs Core Contributors\n */\ninterface EmissionSchedule {\n\t/**\n\t * @dev Fired in release(), releaseCapped(), releaseAll() functions, as well as\n\t *      in releaseAndCall(), releaseAndCallCapped(), releaseAllAndCall() functions\n\t *\n\t * @param target an account where tokens were released to, or ERC1363Receiver smart contract\n\t *       where tokens were sent to and the `onTransferReceived` callback function was triggered on\n\t * @param amount amount of the tokens released\n\t * @param withCallback true if `target` was treated as the ERC1363Receiver smart contract and\n\t *      `onTransferReceived` callback was executed on it; false otherwise\n\t * @param callbackPayload ERC1363 payload passed to the target contract's `onTransferReceived` callback\n\t */\n\tevent Released(address indexed target, uint256 amount, bool withCallback, bytes callbackPayload);\n\n\t/**\n\t * @notice Emission token is an address of the token this schedule releases into\n\t *      circulation, or zero address if schedule releases native currency like ETH\n\t *\n\t * @return Emission token address or zero address\n\t */\n\tfunction emissionToken() external view returns(address);\n\n\t/**\n\t * @notice Emission volume as a function of time `t` (unix timestamp)\n\t *\n\t * @param t current time or just the time of interest, unix timestamp\n\t * @return emission volume, number of tokens which could be released so far (by time `t`)\n\t */\n\tfunction emissionVolume(uint256 t) external view returns(uint256);\n\n\t/**\n\t * @notice Emission volume on the [t0, t1) time interval\n\t *\n\t * @param t0 lower time interval bound, inclusive, unix timestamp\n\t * @param t1 upper time interval bound, exclusive, unix timestamp\n\t * @return emission volume on the [t0, t1) time interval\n\t */\n\tfunction emissionVolumeDelta(uint256 t0, uint256 t1) external view returns(uint256);\n\n\t/**\n\t * @notice Emission rate as a function of time `t` (unix timestamp) and time change `dt` (seconds)\n\t *\n\t * @param t current time or just the time of interest, unix timestamp\n\t * @param dt time change, seconds, can't be zero (default value: 1)\n\t * @return emission rate, which is an emission volume on the [t - dt, t) time interval\n\t */\n\tfunction emissionRate(uint256 t, uint256 dt) external view returns(uint256);\n\n\t/**\n\t * @notice Emission period is the time interval when emission happens, it is defined\n\t *      by the start time, and duration\n\t *\n\t * @notice Implementations MUST use zero duration value for empty schedules, not releasing any tokens;\n\t *      implementations SHOULD return (0, 0) (start, duration) for empty schedules\n\t *\n\t * @return start emission start time, unix timestamp\n\t * @return duration emission duration, seconds\n\t */\n\tfunction emissionPeriod() external view returns(uint256 start, uint256 duration);\n\n\t/**\n\t * @notice Initial emission volume, amount of tokens which can be released immediately\n\t *      when the schedule starts\n\t *\n\t * @return initial emission volume, emissionVolume(0)\n\t */\n\tfunction initialEmissionVolume() external view returns(uint256);\n\n\t/**\n\t * @notice Total / final emission volume, amount of tokens which will be released eventually;\n\t *      amount of tokens which can be released after the schedule ends\n\t *\n\t * @return total (final) emission volume, emissionVolume(start time + duration)\n\t */\n\tfunction totalEmissionVolume() external view returns(uint256);\n\n\t/**\n\t * @notice Amount of tokens already released\n\t *\n\t * @notice Note: this can be lower than the value returned by `emissionVolume(now)`\n\t *      as not all the tokens which could be released, may be released\n\t *\n\t * @return amount of tokens already released\n\t */\n\tfunction alreadyReleasedAmount() external view returns(uint256);\n\n\t/**\n\t * @notice Amount of tokens that can be (will be) released in the future\n\t *\n\t * @return amount of tokens yet to be released\n\t */\n\tfunction toBeReleasedAmount() external view returns(uint256);\n\n\t/**\n\t * @notice Amount of tokes that can be released right now\n\t *\n\t * @dev Implementation MUST guarantee that this is the amount which:\n\t *      - can be safely passed to release(to, amount) function and it won't throw\n\t *      - is released and returned by the releaseCapped(to, cap) when releasing with no cap, or\n\t *            with the higher cap than the canReleaseNowAmount()\n\t *      - is released and returned by the releaseAll(to)\n\t *\n\t * @return amount of tokens that can be released right now\n\t */\n\tfunction canReleaseNowAmount() external view returns(uint256);\n\n\t/**\n\t * @notice Releases specified amount of tokens to the specified destination address `to`,\n\t *      throwing if the amount is either zero or exceeds canReleaseNowAmount()\n\t *\n\t * @dev Implementation MUST throw if the specified amount exceeds canReleaseNowAmount()\n\t *\n\t * @param to an address to release tokens to\n\t * @param amount the amount of tokens to release\n\t */\n\tfunction release(address to, uint256 amount) external;\n\n\t/**\n\t * @notice Releases tokens available for releasing, but no more than the specified cap\n\t *\n\t * @param to an address to release (send) tokens to\n\t * @param cap maximum amount of tokens to release (amount cap); zero means no cap\n\t * @return releasedAmount the actual released amount, not exceeding neither `cap`, nor canReleaseNowAmount()\n\t */\n\tfunction releaseCapped(address to, uint256 cap) external returns(uint256 releasedAmount);\n\n\t/**\n\t * @notice Releases all the tokens available for releasing\n\t *\n\t * @param to an address to release (send) tokens to\n\t * @return releasedAmount the actual released amount, not exceeding canReleaseNowAmount()\n\t */\n\tfunction releaseAll(address to) external returns(uint256 releasedAmount);\n\n\t/**\n\t * @notice Releases specified amount of tokens to the specified ERC1361Receiver contract,\n\t *      throwing if the amount specified is either zero or exceeds canReleaseNowAmount()\n\t *\n\t * @notice Executes the ERC1361 `onTransferReceived` callback function on the target contract\n\t *\n\t * @dev Implementation MUST throw if the amount specified exceeds canReleaseNowAmount()\n\t *\n\t * @param target ERC1363Receiver smart contract to send released tokens to,\n\t *       and trigger the `onTransferReceived` callback function on\n\t * @param amount the amount of tokens to release\n\t * @param data ERC1363 payload to be passed to the target contract's `onTransferReceived` callback\n\t */\n\tfunction releaseAndCall(address target, uint256 amount, bytes calldata data) external;\n\n\t/**\n\t * @notice Releases tokens available for releasing, but no more than the cap specified\n\t *\n\t * @notice Executes the ERC1361 `onTransferReceived` callback function on the target contract\n\t *\n\t * @param target ERC1363Receiver smart contract to send released tokens to,\n\t *       and trigger the `onTransferReceived` callback function on\n\t * @param cap maximum amount of tokens to release (amount cap); zero means no cap\n\t * @param data ERC1363 payload to be passed to the target contract's `onTransferReceived` callback\n\t * @return releasedAmount the actual released amount, not exceeding neither `cap`, nor canReleaseNowAmount()\n\t */\n\tfunction releaseAndCallCapped(address target, uint256 cap, bytes calldata data) external returns(uint256 releasedAmount);\n\n\t/**\n\t * @notice Releases all the tokens available for releasing\n\t *\n\t * @notice Executes the ERC1361 callback `onTransferReceived` on the target contract\n\t *\n\t * @param target ERC1363Receiver smart contract to send released tokens to,\n\t *       and trigger the `onTransferReceived` callback function on\n\t * @param data ERC1363 payload to be passed to the target contract's `onTransferReceived` callback\n\t * @return releasedAmount the actual released amount, not exceeding canReleaseNowAmount()\n\t */\n\tfunction releaseAllAndCall(address target, bytes calldata data) external returns(uint256 releasedAmount);\n}\n"
    },
    "contracts/protocol/multicall/Multicall.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.4;\n\nimport \"@openzeppelin/contracts/utils/Address.sol\";\n\n/**\n * @title Multicall\n *\n * @notice Simplified version of the OZ Multicall contract\n *\n * @dev The use case of the Multicall is to be inherited by the contacts\n *      wishing to allow their clients to execute several functions in a batch\n *\n * @dev Executes in the self-context (storage) via `delegatecall`\n *\n * @author OpenZeppelin\n * @author Lizard Labs Core Contributors\n */\nabstract contract Multicall {\n\t/**\n\t * @dev Multicall support: a function to batch together multiple calls in a single external call.\n\t * @dev Receives and executes a batch of function calls on this contract.\n\t *\n\t * @param data an array of ABI-encoded function calls\n\t * @return results an array of ABI-encoded results of the function calls\n\t */\n\tfunction multicall(bytes[] calldata data) external virtual returns (bytes[] memory results) {\n\t\t// the implementation is copied from OZ Multicall contract;\n\t\t// Context-related stuff is dropped as it's not supported by this contract\n\t\tresults = new bytes[](data.length);\n\t\tfor (uint256 i = 0; i < data.length; i++) {\n\t\t\tresults[i] = Address.functionDelegateCall(address(this), data[i]);\n\t\t}\n\t\treturn results;\n\t}\n}\n"
    },
    "contracts/protocol/tk_dis/MerkleTokenDistributor.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.4;\n\nimport \"@lazy-sol/access-control-upgradeable/contracts/UpgradeableAccessControlCore.sol\";\nimport \"@openzeppelin/contracts/utils/cryptography/MerkleProof.sol\";\n\nimport \"../../utils/EIP712Support.sol\";\nimport \"../multicall/Multicall.sol\";\n\n/**\n * @title Merkle Token Distributor\n *\n * @notice Distributes the ERC20 tokens among a large number of participants in a scalable way\n *      via the pull mechanism (when participants come and claim).\n *\n * @dev The scalability is achieved by storing the claim data (who earned what / how much)\n *      off-chain in a Merkle tree data structure, and allowing the participants to claim\n *      by presenting relevant piece of the data along with the corresponding Merkle proof,\n *      which is verified by the smart contract using the data Merkle root stored on-chain.\n *\n * @dev The claim data is managed fully off-chain. The Merkle root of the off-chain data gets\n *      written into the contract every time the off-chain data is updated.\n *\n * @dev Due to its centralized nature (the claims are decided and managed off-chain), the contract\n *      is designed to support the release of the tokens in a flexible way, including the\n *      EmissionSchedule mechanism, which guarantees no tokens are released (minted or sent\n *      from the treasury) above the scheduled amounts.\n *\n * @dev Actual token release mechanism is defined and implemented by the concrete implementations,\n *      inheriting from this contract.\n *\n * @dev Merkle proof verification is based on OpenZeppelin implementation, see\n *      https://docs.openzeppelin.com/contracts/4.x/api/utils#MerkleProof\n *\n * @author Lizard Labs Core Contributors\n */\nabstract contract MerkleTokenDistributor is UpgradeableAccessControlCore, EIP712Support, Multicall {\n\t// Use Zeppelin MerkleProof Library to verify Merkle proofs\n\tusing MerkleProof for bytes32[];\n\n\t/**\n\t * @dev Tokens claim request, formed and signed by the eligible user, is used in\n\t *      claimWithAuth() EIP712 variant of the claimTo() function\n\t * @dev The request contains all the same fields non-EIP712 claimAndCall() function\n\t *      expects, plus EIP712 specific request valid after/before dates\n\t * @dev See claimAndCall() function for the detailed description of the parameters\n\t */\n\tstruct ClaimRequest {\n\t\t/// @dev an address to send claimed tokens to, or ERC1363Receiver smart contract to send claimed tokens to\n\t\t///      and trigger the `onTransferReceived` callback function on\n\t\taddress target;\n\t\t/// @dev treat `target` as the ERC1363Receiver smart contract and execute `onTransferReceived` callback on it\n\t\tbool withCallback;\n\t\t/// @dev ERC1363 payload to be passed to the target contract's `onTransferReceived` callback\n\t\tbytes callbackPayload;\n\t\t/// @dev the claim data entry to claim\n\t\tClaim claim;\n\t\t/// @dev request valid from date, exclusive, unix timestamp\n\t\tuint32 validAfter;\n\t\t/// @dev request valid until date, exclusive, unix timestamp\n\t\tuint32 validBefore;\n\t}\n\n\t/**\n\t * @dev Cumulative claim data entry defines how much `amount` of the token `token`\n\t *      an account `account` is eligible to get\n\t *\n\t * @dev The claim data is a collection of the Claim entries, called the claims dataset\n\t */\n\tstruct Claim {\n\t\t/// @dev Address of the token distributed by this claim\n\t\taddress token;\n\t\t/// @dev Address of the account eligible for the tokens\n\t\taddress account;\n\t\t/// @dev Total amount of the `token` tokens the account is eligible for\n\t\tuint96 amount;\n\t}\n\t\n\t/**\n\t * @notice Claims dataset root, Merkle tree root for the claims dataset\n\t *\n\t * @notice Merkle root effectively \"compresses\" the (potentially) huge collection of elements\n\t *      and allows to store it in a single 256-bits storage slot on-chain\n\t */\n\tbytes32 private root;\n\n\t/**\n\t * @dev At any point in time any account can come and claim the difference of what\n\t *     is stored off-chain in the claims dataset (and \"pinned\" on-chain in a `root`)\n\t *     and what is recorded as already claimed on-chain\n\t *\n\t * @dev Already claimed tokens mapping\n\t *      maps keccak256(token address, account address) => total amount claimed\n\t */\n\tmapping(bytes32 => uint96) private claimed;\n\n\t/**\n\t * @dev Empty reserved space in storage. The size of the __gap array is calculated so that\n\t *      the amount of storage used by a contract always adds up to the 50.\n\t *      See https://docs.openzeppelin.com/contracts/4.x/upgradeable#storage_gaps\n\t */\n\tuint256[48] private __gap;\n\n\t/**\n\t * @notice Enables token claiming functionality (`claim`, `claimTo`)\n\t *\n\t * @dev Feature FEATURE_CLAIMING_ENABLED is required for the claim() and claimTo()\n\t *      functions to succeed\n\t */\n\tuint32 public constant FEATURE_CLAIMING_ENABLED = 0x0000_0004;\n\n\t/**\n\t * @notice Enables the EIP712-signed meta token claiming functionality\n\t *\n\t * @dev Feature FEATURE_EIP712_CLAIMING_ENABLED is required for the\n\t *      claimWithAuth() function to succeed\n\t */\n\tuint32 public constant FEATURE_EIP712_CLAIMING_ENABLED = 0x0000_0008;\n\n\t/**\n\t * @notice Enables the ERC1363 callable transfers extension, allowing clients to\n\t *      execute arbitrary callbacks on the contracts they are sending tokens to\n\t *\n\t * @dev Feature FEATURE_ERC1363_EXECUTION_ENABLED is required for claimAndCall()\n\t *      function to succeed\n\t */\n\tuint32 public constant FEATURE_ERC1363_EXECUTION_ENABLED = 0x0000_0010;\n\n\t/**\n\t * @notice Data manager is responsible for supplying the valid claims dataset root,\n\t *      Merkle root which then can be used to claim tokens, meaning effectively,\n\t *      that data manager may act as a token releaser on the target EmissionSchedule contract\n\t *\n\t * @dev Role ROLE_DATA_MANAGER allows setting the Merkle tree root via setInputDataRoot()\n\t */\n\tuint32 public constant ROLE_DATA_MANAGER = 0x0002_0000;\n\n\t/**\n\t * @notice EIP-712 delegation struct typeHash,\n\t *      see https://eips.ethereum.org/EIPS/eip-712#rationale-for-typehash\n\t */\n\t// keccak256(\"ClaimRequest(address target,bool withCallback,bytes callbackPayload,Claim claim,uint32 validAfter,uint32 validBefore)Claim(address token,address account,uint96 amount)\")\n\tbytes32 public constant CLAIM_REQUEST_TYPEHASH = 0xd5aa95facbd0e115a6ae9b2fd52b321e16b3eed28873faadbceca1c68da48434;\n\n\t/**\n\t * @notice EIP-712 delegation struct typeHash,\n\t *      see https://eips.ethereum.org/EIPS/eip-712#rationale-for-typehash\n\t */\n\t// keccak256(\"Claim(address token,address account,uint96 amount)\")\n\tbytes32 public constant CLAIM_TYPEHASH = 0xd46cc54961102d0eea3e85ff9b6d75d156b3246300f5e303b31e6c1cb54a1972;\n\n\t/**\n\t * @dev Fired in setInputDataRoot()\n\t *\n\t * @param by an address which executed the operation\n\t * @param root new Merkle root value\n\t */\n\tevent RootChanged(address indexed by, bytes32 root);\n\n\t/**\n\t * @dev Fired in claim(), claimTo(), claimAndCall(), and claimWithAuth() functions\n\t *\n\t * @param token address of the token claimed\n\t * @param account account whose tokens were claimed\n\t * @param target an account where tokens were sent to, or ERC1363Receiver smart contract\n\t *       where tokens were sent to and the `onTransferReceived` callback function was triggered on\n\t * @param amount the amount claimed (within the current transaction)\n\t * @param totalAmount the total amount claimed (as in claim data entry)\n\t * @param withCallback true if `target` was treated as the ERC1363Receiver smart contract and\n\t *      `onTransferReceived` callback was executed on it; false otherwise\n\t * @param callbackPayload ERC1363 payload passed to the target contract's `onTransferReceived` callback\n\t */\n\tevent TokensClaimed(\n\t\taddress indexed token,\n\t\taddress indexed account,\n\t\taddress indexed target,\n\t\tuint96 amount,\n\t\tuint96 totalAmount,\n\t\tbool withCallback,\n\t\tbytes callbackPayload\n\t);\n\n\t/**\n\t * @notice Getter for the claims dataset root, Merkle tree root for the claims dataset\n\t *\n\t * @return Merkle tree root for the claims dataset\n\t */\n\tfunction getInputDataRoot() public view returns(bytes32) {\n\t\t// read Merkle root from storage and return\n\t\treturn root;\n\t}\n\n\t/**\n\t * @notice Restricted access function to update claims dataset root (Merkle tree root),\n\t *       and to update, effectively, the claims dataset itself\n\t *\n\t * @dev Requires executor to have `ROLE_DATA_MANAGER` permission\n\t *\n\t * @param _root Merkle tree root for the claims dataset\n\t */\n\tfunction setInputDataRoot(bytes32 _root) public virtual {\n\t\t// verify the access permission\n\t\t_requireSenderInRole(ROLE_DATA_MANAGER);\n\n\t\t// update input data Merkle tree root\n\t\troot = _root;\n\n\t\t// emit an event\n\t\temit RootChanged(msg.sender, _root);\n\t}\n\n\t/**\n\t * @notice Verifies the validity of the claim data entry supplied (namely, if `root` contains it)\n\t *      based on the Merkle root of the plot data collection (already defined on the contract),\n\t *      and the Merkle proof supplied to validate the particular plot data\n\t *\n\t * @dev Merkle tree and proof can be constructed using the `web3-utils`, `merkletreejs`,\n\t *      and `keccak256` npm packages:\n\t *      1. Hash the claim data collection elements via `web3.utils.soliditySha3`, making sure\n\t *         the packing order and types are exactly as defined in `Claim` struct\n\t *      2. Create a sorted MerkleTree (`merkletreejs`) from the hashed collection, use `keccak256`\n\t *         from the `keccak256` npm package as a hashing function, do not hash leaves\n\t *         (already hashed in step 1); Ex. MerkleTree options: {hashLeaves: false, sortPairs: true}\n\t *      3. For any given plot data element the proof is constructed by hashing it (as in step 1),\n\t *         and querying the MerkleTree for a proof, providing the hashed plot data element as a leaf\n\t *\n\t * @dev See also: https://docs.openzeppelin.com/contracts/4.x/api/utils#MerkleProof\n\t *\n\t * @param _claim claim data entry to verify\n\t * @param proof Merkle proof for the claim data entry supplied\n\t * @return true if the claim is valid (belongs to registered collection), false otherwise\n\t */\n\tfunction isClaimValid(Claim memory _claim, bytes32[] memory proof) public view returns(bool) {\n\t\t// construct Merkle tree leaf from the inputs supplied\n\t\tbytes32 leaf = keccak256(abi.encodePacked(\n\t\t\t_claim.token,\n\t\t\t_claim.account,\n\t\t\t_claim.amount\n\t\t));\n\n\t\t// cache Merkle root on stack\n\t\tbytes32 _root = root;\n\n\t\t// make sure claims dataset root was set\n\t\trequire(_root != 0x00, \"empty root\");\n\n\t\t// verify the proof supplied, and return the verification result\n\t\treturn proof.verify(_root, leaf);\n\t}\n\n\t/**\n\t * @notice Get currently claimed token `tokenAddress` cumulative amount for the account `accountAddress`\n\t *\n\t * @param tokenAddress claimed token address\n\t * @param accountAddress user account address\n\t * @return total token amount claimed for the user account\n\t */\n\tfunction getClaimed(address tokenAddress, address accountAddress) public view returns(uint96) {\n\t\t// calculate the key, read from storage, and return\n\t\treturn claimed[keccak256(abi.encodePacked(tokenAddress, accountAddress))];\n\t}\n\n\t/**\n\t * @notice Claims the tokens calculated off-chain and delivered to the contract\n\t *      in the claim data entry `claim` with the claims dataset belonging proof\n\t *\n\t * @notice Sends claimed tokens to the transaction executor\n\t *\n\t * @dev Merkle tree and proof can be constructed using the `web3-utils`, `merkletreejs`,\n\t *      and `keccak256` npm packages:\n\t *      1. Hash the claim data collection elements via `web3.utils.soliditySha3`, making sure\n\t *         the packing order and types are exactly as defined in `Claim` struct\n\t *      2. Create a sorted MerkleTree (`merkletreejs`) from the hashed collection, use `keccak256`\n\t *         from the `keccak256` npm package as a hashing function, do not hash leaves\n\t *         (already hashed in step 1); Ex. MerkleTree options: {hashLeaves: false, sortPairs: true}\n\t *      3. For any given plot data element the proof is constructed by hashing it (as in step 1),\n\t *         and querying the MerkleTree for a proof, providing the hashed plot data element as a leaf\n\t *\n\t * @dev See also: https://docs.openzeppelin.com/contracts/4.x/api/utils#MerkleProof\n\t *\n\t * @param _claim claim data entry to verify and fulfill\n\t * @param proof Merkle proof for the claim data entry supplied\n\t */\n\tfunction claim(Claim memory _claim, bytes32[] memory proof) public {\n\t\t// delegate to `claimTo`\n\t\tclaimTo(msg.sender, _claim, proof);\n\t}\n\t\n\t/**\n\t * @notice Claims the tokens calculated off-chain and delivered to the contract\n\t *      in the claim data entry `claim` with the claims dataset belonging proof\n\t *\n\t * @notice Sends claimed tokens to the address specified in the function param `to`\n\t *\n\t * @dev Merkle tree and proof can be constructed using the `web3-utils`, `merkletreejs`,\n\t *      and `keccak256` npm packages:\n\t *      1. Hash the claim data collection elements via `web3.utils.soliditySha3`, making sure\n\t *         the packing order and types are exactly as defined in `Claim` struct\n\t *      2. Create a sorted MerkleTree (`merkletreejs`) from the hashed collection, use `keccak256`\n\t *         from the `keccak256` npm package as a hashing function, do not hash leaves\n\t *         (already hashed in step 1); Ex. MerkleTree options: {hashLeaves: false, sortPairs: true}\n\t *      3. For any given plot data element the proof is constructed by hashing it (as in step 1),\n\t *         and querying the MerkleTree for a proof, providing the hashed plot data element as a leaf\n\t *\n\t * @dev See also: https://docs.openzeppelin.com/contracts/4.x/api/utils#MerkleProof\n\t *\n\t * @param to an address to send claimed tokens to\n\t * @param _claim claim data entry to verify and fulfill\n\t * @param proof Merkle proof for the claim data entry supplied\n\t */\n\tfunction claimTo(address to, Claim memory _claim, bytes32[] memory proof) public {\n\t\t// verify claiming is active\n\t\trequire(_isFeatureEnabled(FEATURE_CLAIMING_ENABLED), \"claiming disabled\");\n\n\t\t// verify claiming is done by the claim owner\n\t\t_requireAccessCondition(_claim.account == msg.sender);\n\n\t\t// delegate to `__claim` with the ERC1363 payload switch disabled\n\t\t__claim(to, _claim, proof, false, \"\");\n\t}\n\n\t/**\n\t * @notice Claims the tokens calculated off-chain and delivered to the contract\n\t *      in the claim data entry `claim` with the claims dataset belonging proof\n\t *\n\t * @dev Sends claimed tokens to the specified ERC1361Receiver contract and executes the\n\t *      `onTransferReceived` callback on it\n\t *\n\t * @dev To send claimed tokens to the staking contract,\n\t *      1) set the `target` address to the staking contract address and\n\t *      2) set the bytes `data` field to ERC1363 depositTo() payload on the staking contract\n\t *      3) specify the deposit owner in the ERC1363 depositTo() payload\n\t *\n\t * @dev Merkle tree and proof can be constructed using the `web3-utils`, `merkletreejs`,\n\t *      and `keccak256` npm packages:\n\t *      1. Hash the claim data collection elements via `web3.utils.soliditySha3`, making sure\n\t *         the packing order and types are exactly as defined in `Claim` struct\n\t *      2. Create a sorted MerkleTree (`merkletreejs`) from the hashed collection, use `keccak256`\n\t *         from the `keccak256` npm package as a hashing function, do not hash leaves\n\t *         (already hashed in step 1); Ex. MerkleTree options: {hashLeaves: false, sortPairs: true}\n\t *      3. For any given plot data element the proof is constructed by hashing it (as in step 1),\n\t *         and querying the MerkleTree for a proof, providing the hashed plot data element as a leaf\n\t *\n\t * @dev See also: https://docs.openzeppelin.com/contracts/4.x/api/utils#MerkleProof\n\t *\n\t * @param target ERC1363Receiver smart contract to send claimed tokens to,\n\t *       and trigger the `onTransferReceived` callback function on\n\t * @param _claim claim data entry to verify and fulfill\n\t * @param proof Merkle proof for the claim data entry supplied\n\t * @param data ERC1363 payload to be passed to the target contract's `onTransferReceived` callback\n\t */\n\tfunction claimAndCall(address target, Claim memory _claim, bytes32[] memory proof, bytes memory data) public {\n\t\t// verify claiming is active\n\t\trequire(_isFeatureEnabled(FEATURE_CLAIMING_ENABLED), \"claiming disabled\");\n\n\t\t// verify claiming is done by the claim owner\n\t\t_requireAccessCondition(_claim.account == msg.sender);\n\n\t\t// delegate to `__claim` with the ERC1363 payload switch enabled\n\t\t__claim(target, _claim, proof, true, data);\n\t}\n\n\t/**\n\t * @notice Claims the tokens calculated off-chain and delivered to the contract\n\t *      in the claim data entry `claim` with the claims dataset belonging proof\n\t *\n\t * @notice Sends claimed tokens to the address or contract specified in the request param `to`,\n\t *      and optionally executes the ERC1361 `onTransferReceived` callback on the target contract\n\t *\n\t * @dev To send claimed tokens to the staking contract,\n\t *      1) set the `to` address to the staking contract address and\n\t *      2) set the `withCallback` switch flag to true\n\t *      3) set the bytes `callbackPayload` field to ERC1363 depositTo() payload on the staking contract\n\t *      4) specify the deposit owner in the ERC1363 depositTo() payload\n\t *\n\t * @notice Requires `claim.account` to sign the ClaimRequest struct relayed to this function\n\t *\n\t * @dev Requires:\n\t *     - `exp` to be a timestamp in the future\n\t *     - `v`, `r` and `s` to be a valid `secp256k1` signature from `accountAddress`\n\t *        over the EIP712-formatted function arguments.\n\t *\n\t * @dev Merkle tree and proof can be constructed using the `web3-utils`, `merkletreejs`,\n\t *      and `keccak256` npm packages:\n\t *      1. Hash the claim data collection elements via `web3.utils.soliditySha3`, making sure\n\t *         the packing order and types are exactly as defined in `Claim` struct\n\t *      2. Create a sorted MerkleTree (`merkletreejs`) from the hashed collection, use `keccak256`\n\t *         from the `keccak256` npm package as a hashing function, do not hash leaves\n\t *         (already hashed in step 1); Ex. MerkleTree options: {hashLeaves: false, sortPairs: true}\n\t *      3. For any given plot data element the proof is constructed by hashing it (as in step 1),\n\t *         and querying the MerkleTree for a proof, providing the hashed plot data element as a leaf\n\t *\n\t * @dev See also: https://docs.openzeppelin.com/contracts/4.x/api/utils#MerkleProof\n\t *\n\t * @param req tokens claim request, containing the claim data entry to verify\n\t * @param proof Merkle proof for the claims data entry supplied\n\t * @param sig ECDSA signature of the request\n\t */\n\tfunction claimWithAuth(ClaimRequest calldata req, bytes32[] calldata proof, Signature calldata sig) public {\n\t\t// verify EIP712 claiming is active\n\t\trequire(_isFeatureEnabled(FEATURE_EIP712_CLAIMING_ENABLED), \"EIP712 claiming disabled\");\n\n\t\t// extract the claim for easier access\n\t\tClaim calldata _claim = req.claim;\n\n\t\t// derive the signer of the request\n\t\taddress signer = __deriveSigner(req, sig);\n\n\t\t// verify the request (nonce, validity dates, etc.)\n\t\trequire(signer == _claim.account, \"invalid signature\");\n\t\trequire(now32() > req.validAfter, \"signature not yet valid\");\n\t\trequire(now32() < req.validBefore, \"signature expired\");\n\n\t\t// delegate to `__claim` with the ERC1363 payload switch as in the request\n\t\t__claim(req.target, _claim, proof, req.withCallback, req.callbackPayload);\n\t}\n\n\t/**\n\t * @dev Release claim() and claimAndCall() logic implementation\n\t *\n\t * @param target an address to send claimed tokens to, or\n\t *       ERC1363Receiver smart contract to send claimed tokens to,\n\t *       and trigger the `onTransferReceived` callback function on\n\t * @param _claim claim data entry to verify\n\t * @param proof Merkle proof for the claim data entry supplied\n\t * @param withPayload either treat the `releaseTo` as ERC1363Receiver and trigger the\n\t *       `onTransferReceived` callback on it (true) or not (false)\n\t * @param data ERC1363 payload to be passed to the target contract's `onTransferReceived` callback\n\t */\n\tfunction __claim(address target, Claim memory _claim, bytes32[] memory proof, bool withPayload, bytes memory data) private {\n\t\t// verify the claim data entry supplied is valid\n\t\trequire(isClaimValid(_claim, proof), \"invalid claim\");\n\n\t\t// cache the mapping key on stack\n\t\tbytes32 key = keccak256(abi.encodePacked(_claim.token, _claim.account));\n\t\t// cache current cumulative claimed value on stack\n\t\tuint96 _claimed = claimed[key];\n\n\t\t// make sure there is something to claim\n\t\trequire(_claimed < _claim.amount, \"nothing to claim\");\n\n\t\t// update current cumulative claimed value\n\t\tclaimed[key] = _claim.amount;\n\n\t\t// how much tokens we will have to transfer to the beneficiary\n\t\tuint96 payoutAmount = _claim.amount - _claimed;\n\n\t\t// if the ERC1363 release with callback is requested\n\t\tif(withPayload) {\n\t\t\t// verify if ERC1363 callable transfers are enabled\n\t\t\trequire(_isFeatureEnabled(FEATURE_ERC1363_EXECUTION_ENABLED), \"ERC1363 execution disabled\");\n\t\t\t// ERC1363 release: delegate to `__releaseAndCall()`\n\t\t\t__releaseAndCall(_claim.token, target, payoutAmount, data);\n\t\t\t// emit an event with the ERC1363 payload\n\t\t\temit TokensClaimed(_claim.token, _claim.account, target, payoutAmount, _claim.amount, true, data);\n\t\t}\n\t\t\t// otherwise execute simple ERC20 release\n\t\telse {\n\t\t\t// release: delegate to `__release()`\n\t\t\t__release(_claim.token, target, payoutAmount);\n\t\t\t// emit an event without the ERC1363 payload\n\t\t\temit TokensClaimed(_claim.token, _claim.account, target, payoutAmount, _claim.amount, false, \"\");\n\t\t}\n\t}\n\n\t/**\n\t * @dev Releases the specified amount `amount` of the token `token` to the address `to`\n\t *      using the implementation specific mechanism (ex.: via EmissionSchedule)\n\t *\n\t * @param token address of the token to release\n\t * @param to destination address to release the specified token to\n\t * @param amount amount of the specified token to release to the specified address\n\t */\n\tfunction __release(address token, address to, uint256 amount) internal virtual;\n\n\t/**\n\t * @dev Releases the specified amount `amount` of the token `token` to the `target` contract\n\t *      using the implementation specific mechanism (ex.: via EmissionSchedule)\n\t *\n\t * @dev Sends released tokens to the specified ERC1361Receiver contract and executes the\n\t *      `onTransferReceived` callback on it\n\t *\n\t * @param token address of the token to release\n\t * @param target ERC1363Receiver smart contract to send released tokens to,\n\t *       and trigger the `onTransferReceived` callback function on\n\t * @param amount amount of the specified token to release to the specified contract\n\t * @param data ERC1363 payload to be passed to the target contract's `onTransferReceived` callback\n\t */\n\tfunction __releaseAndCall(address token, address target, uint256 amount, bytes memory data) internal virtual;\n\n\t/**\n\t * @dev Auxiliary function to verify structured EIP712 message signature and derive its signer\n\t *\n\t * @dev Recovers the non-zero signer address from the signed message throwing on failure\n\t *\n\t * @param req tokens claim request, signed by the staker\n\t * @param sig ECDSA signature of the request\n\t * @return recovered non-zero signer address, unless throwing\n\t */\n\tfunction __deriveSigner(ClaimRequest calldata req, Signature calldata sig) private view returns(address) {\n\t\t// build the EIP-712 hashStruct of the message\n\t\tbytes32 hashStruct = keccak256(abi.encode(\n\t\t\tCLAIM_REQUEST_TYPEHASH,\n\t\t\treq.target,\n\t\t\treq.withCallback,\n\t\t\tkeccak256(req.callbackPayload),\n\t\t\tkeccak256(abi.encode(CLAIM_TYPEHASH, req.claim)),\n\t\t\treq.validAfter,\n\t\t\treq.validBefore\n\t\t));\n\n\t\t// derive the signer of the EIP712 message\n\t\treturn __deriveSigner(hashStruct, sig);\n\t}\n\n\t/**\n\t * @dev Testing time-dependent functionality may be difficult;\n\t *      we override time in the helper test smart contract (mock)\n\t *\n\t * @return `block.timestamp` in mainnet, custom values in testnets (if overridden)\n\t */\n\tfunction now32() public view virtual returns(uint32) {\n\t\t// return current block timestamp\n\t\treturn uint32(block.timestamp);\n\t}\n}\n"
    },
    "contracts/protocol/tk_dis/ScheduledTokenDistributorV1.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.4;\n\nimport \"../es/EmissionSchedule.sol\";\nimport \"./MerkleTokenDistributor.sol\";\n\n/**\n * @title Scheduled Token Distributor\n *\n * @dev MerkleTokenDistributor implementation, see MerkleTokenDistributor for more details\n *\n * @dev The contract is designed to get the tokens from the EmissionSchedule instance,\n *      which guarantees no tokens are released above the scheduled amounts.\n *\n * @author Lizard Labs Core Contributors\n */\ncontract ScheduledTokenDistributorV1 is MerkleTokenDistributor {\n\t/**\n\t * @dev Emission schedules are used to release the tokens, helping to guarantee\n\t *      no tokens are released above the scheduled amounts\n\t *\n\t * @dev Emission schedules per token\n\t *      maps token address => emission schedule for the token\n\t */\n\tmapping(address => EmissionSchedule) private schedules;\n\n\t/**\n\t * @notice Emission manager is responsible for registering, updating, and deleting\n\t *      emission schedules for the tokens\n\t *\n\t * @dev Role ROLE_EMISSION_MANAGER allows executing updateEmissionSchedule() and\n\t *      deleteEmissionSchedule() functions\n\t */\n\tuint32 public constant ROLE_EMISSION_MANAGER = 0x0001_0000;\n\n\t/**\n\t * @dev Fired in deleteEmissionSchedule()\n\t *\n\t * @param token token's address whose emission schedule was deleted\n\t */\n\tevent EmissionScheduleDeleted(address indexed token);\n\n\t/**\n\t * @dev Fired in updateEmissionSchedule()\n\t *\n\t * @param token token's address whose emission schedule was registered/updated\n\t * @param schedule address of the emission schedule associated with the token \n\t */\n\tevent EmissionScheduleUpdated(address indexed token, address indexed schedule);\n\n\t/**\n\t * @inheritdoc EIP712Support\n\t */\n\tfunction DOMAIN_NAME() public pure override returns(string memory) {\n\t\t// simply return the contract name constant\n\t\treturn \"ScheduledTokenDistributorV1\";\n\t}\n\n\t/**\n\t * @dev \"Constructor replacement\" for a smart contract with a delayed initialization (post-deployment initialization)\n\t *\n\t * @dev Initializes the RBAC module setting the tx sender as an admin\n\t *\n\t * @param tokenAddress ERC-20 token address, serving as the default claimable token\n\t * @param emissionScheduleAddress address of the emission schedule used to release the tokens\n\t */\n\tfunction postConstruct(address tokenAddress, address emissionScheduleAddress) public initializer {\n\t\t// verify the inputs are set\n\t\trequire(tokenAddress != address(0) && emissionScheduleAddress != address(0), \"zero address\");\n\n\t\t// initialize contract storage: initialize default emission schedule\n\t\t__updateEmissionSchedule(tokenAddress, emissionScheduleAddress);\n\n\t\t// initialize the RBAC module\n\t\t_postConstruct(msg.sender, 0);\n\t}\n\n\t/**\n\t * @notice Emission schedule for the token getter\n\t *\n\t * @param tokenAddress address of the token to query the schedule for\n\t * @return emission schedule for the given token\n\t */\n\tfunction getEmissionSchedule(address tokenAddress) public view returns(EmissionSchedule) {\n\t\t// read from the storage and return\n\t\treturn schedules[tokenAddress];\n\t}\n\n\t/**\n\t * @notice Restricted access function to delete the emission schedule for the token\n\t *\n\t * @param tokenAddress token address to delete schedule for\n\t */\n\tfunction deleteEmissionSchedule(address tokenAddress) public {\n\t\t// verify the access permission\n\t\t_requireSenderInRole(ROLE_EMISSION_MANAGER);\n\n\t\t// delete the schedule\n\t\tdelete schedules[tokenAddress];\n\n\t\t// emit an event\n\t\temit EmissionScheduleDeleted(tokenAddress);\n\t}\n\n\t/**\n\t * @notice Restricted access function to register or update the emission schedule for the token\n\t *\n\t * @param tokenAddress token address to update schedule for\n\t * @param emissionScheduleAddress EmissionSchedule address\n\t */\n\tfunction updateEmissionSchedule(address tokenAddress, address emissionScheduleAddress) public {\n\t\t// verify the access permission\n\t\t_requireSenderInRole(ROLE_EMISSION_MANAGER);\n\n\t\t// delegate to unsafe `__updateEmissionSchedule`\n\t\t__updateEmissionSchedule(tokenAddress, emissionScheduleAddress);\n\t}\n\n\t/**\n\t * @dev Updates emission schedule for the token\n\t *      Unsafe. Doesn't verify the access permissions. Must be kept private at all times.\n\t *\n\t * @param tokenAddress token address to update schedule for\n\t * @param emissionScheduleAddress EmissionSchedule address\n\t */\n\tfunction __updateEmissionSchedule(address tokenAddress, address emissionScheduleAddress) private {\n\t\t// connect to the emission schedule\n\t\tEmissionSchedule schedule = EmissionSchedule(emissionScheduleAddress);\n\n\t\t// verify the emission token on the schedule is correct\n\t\trequire(tokenAddress == schedule.emissionToken(), \"unexpected emission token\");\n\n\t\t// update the schedule\n\t\tschedules[tokenAddress] = schedule;\n\n\t\t// emit an event\n\t\temit EmissionScheduleUpdated(tokenAddress, emissionScheduleAddress);\n\t}\n\n\t/**\n\t * @dev Releases the specified amount `amount` of the token `token` to the address `to`\n\t *      using the EmissionSchedule release() function\n\t *\n\t * @inheritdoc MerkleTokenDistributor\n\t */\n\tfunction __release(address token, address to, uint256 amount) internal override {\n\t\t// verify emission schedule exists for the given token\n\t\tEmissionSchedule schedule = schedules[token];\n\t\trequire(address(schedule) != address(0), \"no schedule registered\");\n\n\t\t// transfer the token out\n\t\tschedule.release(to, amount);\n\t}\n\n\t/**\n\t * @dev Releases the specified amount `amount` of the token `token` to the `target` contract\n\t *      using the EmissionSchedule releaseAndCall() function\n\t *\n\t * @inheritdoc MerkleTokenDistributor\n\t */\n\tfunction __releaseAndCall(address token, address target, uint256 amount, bytes memory data) internal override {\n\t\t// verify emission schedule exists for the given token\n\t\tEmissionSchedule schedule = schedules[token];\n\t\trequire(address(schedule) != address(0), \"no schedule registered\");\n\n\t\t// transfer the token out and execute the ERC1363 callback on the target\n\t\t schedule.releaseAndCall(target, amount, data);\n\t}\n}\n"
    },
    "contracts/utils/EIP712Support.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity ^0.8.4;\n\nimport \"@lazy-sol/advanced-erc20/contracts/lib/ECDSA.sol\";\n\n/**\n * @title EIP-712 Support\n *\n * @notice Enables very basic EIP-712 support, helps improving code reuse\n *\n * @author Lizard Labs Core Contributors\n */\nabstract contract EIP712Support {\n\t/// @dev ECDSA signature\n\tstruct Signature {\n\t\t/// @dev v the recovery byte of the signature\n\t\tuint8 v;\n\t\t/// @dev r half of the ECDSA signature pair\n\t\tbytes32 r;\n\t\t/// @dev half of the ECDSA signature pair\n\t\tbytes32 s;\n\t}\n\n\t/**\n\t * @notice EIP-712 contract's domain typeHash,\n\t *      see https://eips.ethereum.org/EIPS/eip-712#rationale-for-typehash\n\t *\n\t * @dev Note: we do not include version into the domain typehash/separator,\n\t *      it is implied version is concatenated to the name field, like \"TokenRewardDistributorV1\"\n\t */\n\t// keccak256(\"EIP712Domain(string name,uint256 chainId,address verifyingContract)\")\n\tbytes32 public constant DOMAIN_TYPEHASH = 0x8cad95687ba82c2ce50e74f7b754645e5117c3a5bec8151c0726d5857980a866;\n\n\t/**\n\t * @notice EIP-712 contract domain name and version concatenated, ex.: MyContractV2\n\t *      see https://eips.ethereum.org/EIPS/eip-712#definition-of-domainseparator\n\t *      note: we specify contract version in its name\n\t */\n\tfunction DOMAIN_NAME() public pure virtual returns(string memory);\n\n\t/**\n\t * @notice EIP-712 contract domain separator,\n\t *      see https://eips.ethereum.org/EIPS/eip-712#definition-of-domainseparator\n\t *      note: we specify contract version in its name\n\t */\n\tfunction DOMAIN_SEPARATOR() public view returns(bytes32) {\n\t\t// build the EIP-712 contract domain separator, see https://eips.ethereum.org/EIPS/eip-712#definition-of-domainseparator\n\t\t// note: we specify contract version in its name\n\t\treturn keccak256(abi.encode(DOMAIN_TYPEHASH, keccak256(bytes(DOMAIN_NAME())), block.chainid, address(this)));\n\t}\n\n\t/**\n\t * @dev Auxiliary function to verify structured EIP712 message signature and derive its signer\n\t *\n\t * @dev Recovers the non-zero signer address from the signed message throwing on failure\n\t *\n\t * @param hashStruct EIP-712 hashStruct of the message\n\t * @param sig ECDSA signature of the message\n\t * @return recovered non-zero signer address, unless throwing\n\t */\n\tfunction __deriveSigner(bytes32 hashStruct, Signature calldata sig) internal view returns(address) {\n\t\t// calculate the EIP-712 digest \"\\x19\\x01\" ‖ domainSeparator ‖ hashStruct(message)\n\t\tbytes32 digest = keccak256(abi.encodePacked(\"\\x19\\x01\", DOMAIN_SEPARATOR(), hashStruct));\n\n\t\t// recover the address which signed the message with v, r, s\n\t\taddress signer = ECDSA.recover(digest, sig.v, sig.r, sig.s);\n\n\t\t// return the signer address derived from the signature\n\t\treturn signer;\n\t}\n}\n"
    }
  }
}}